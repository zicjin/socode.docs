<h1>Contexts</h1>
<blockquote><p><strong>Requirement</strong>: This guide expects that you have gone through the introductory guides and got a Phoenix application up and running.</p></blockquote>
<blockquote><p><strong>Requirement</strong>: This guide expects that you have gone through <a href="request_lifecycle">the Request life-cycle guide</a>.</p></blockquote>
<blockquote><p><strong>Requirement</strong>: This guide expects that you have gone through <a href="ecto">the Ecto guide</a>.</p></blockquote>
<p>So far, we've built pages, wired up controller actions through our routers, and learned how Ecto allows data to be validated and persisted. Now it's time to tie it all together by writing web-facing features that interact with our greater Elixir application.</p>
<p>When building a Phoenix project, we are first and foremost building an Elixir application. Phoenix's job is to provide a web interface into our Elixir application. Naturally, we compose our applications with modules and functions, but simply defining a module with a few functions isn't enough when designing an application. It's vital to think about your application design when writing code. Let's find out how.</p>
<blockquote><p>How to read this guide: Using the context generators is a great way for beginners and intermediate Elixir programmers alike to get up and running quickly while thoughtfully designing their applications. This guide focuses on those readers. On the other hand, experienced developers may get more mileage from nuanced discussions around application design. For those readers, we include a frequently asked questions (FAQ) section at the end of the guide which brings different perspectives to some design decisions made throughout the guide. Beginners can safely skip the FAQ sections and return later when they're ready to dig deeper.</p></blockquote>
<h2 id="thinking-about-design" class="section-heading">  Thinking about design </h2> <p>Contexts are dedicated modules that expose and group related functionality. For example, anytime you call Elixir's standard library, be it <a href="https://hexdocs.pm/logger/Logger.html#info/1"><code class="inline">Logger.info/1</code></a> or <a href="https://hexdocs.pm/elixir/Stream.html#map/2"><code class="inline">Stream.map/2</code></a>, you are accessing different contexts. Internally, Elixir's logger is made of multiple modules, but we never interact with those modules directly. We call the <a href="https://hexdocs.pm/logger/Logger.html"><code class="inline">Logger</code></a> module the context, exactly because it exposes and groups all of the logging functionality.</p>
<p>Phoenix projects are structured like Elixir and any other Elixir project â€“ we split our code into contexts. A context will group related functionality, such as posts and comments, often encapsulating patterns such as data access and data validation. By using contexts, we decouple and isolate our systems into manageable, independent parts.</p>
<p>Let's use these ideas to build out our web application. Our goal is to build a user system as well as a basic content management system for adding and editing page content. Let's get started!</p>
<h3 id="adding-an-accounts-context" class="section-heading">  Adding an Accounts Context </h3> <p>User accounts are often wide-reaching across a platform so it's important to think upfront about writing a well-defined interface. With that in mind, our goal is to build an accounts API that handles creating, updating, and deleting user accounts, as well as authenticating user credentials. We'll start off with basic features, but as we add authentication later, we'll see how starting with a solid foundation allows us to grow our application naturally as we add functionality.</p>
<p>Phoenix includes the <a href="mix.tasks.phx.gen.html"><code class="inline">mix phx.gen.html</code></a>, <a href="mix.tasks.phx.gen.json"><code class="inline">mix phx.gen.json</code></a>, <a href="mix.tasks.phx.gen.live"><code class="inline">mix phx.gen.live</code></a>, and <a href="mix.tasks.phx.gen.context"><code class="inline">mix phx.gen.context</code></a> generators that apply the ideas of isolating functionality in our applications into contexts. These generators are a great way to hit the ground running while Phoenix nudges you in the right direction to grow your application. Let's put these tools to use for our new user accounts context.</p>
<p>In order to run the context generators, we need to come up with a module name that groups the related functionality that we're building. In the <a href="ecto">Ecto guide</a>, we saw how we can use Changesets and Repos to validate and persist user schemas, but we didn't integrate this with our application at large. In fact, we didn't think about where a "user" in our application should live at all. Let's take a step back and think about the different parts of our system. We know that we'll have users of our product. Along with users comes things like account login credentials and user registration. An <code class="inline">Accounts</code> context in our system is a natural place for our user functionality to live.</p>
<blockquote><p>Naming things is hard. If you're stuck when trying to come up with a context name when the grouped functionality in your system isn't yet clear, you can simply use the plural form of the resource you're creating. For example, a <code class="inline">Users</code> context for managing users. As you grow your application and the parts of your system become clear, you can simply rename the context to a more refined name at a later time.</p></blockquote>
<p>Before we use the generators, we need to undo the changes we made in the Ecto guide, so we can give our user schema a proper home. Run these commands to undo our previous work:</p>
<pre data-language="elixir">$ rm lib/hello/user.ex
$ rm priv/repo/migrations/*_create_users.exs</pre>
<p>Next, let's reset our database so we also discard the table we have just removed:</p>
<pre data-language="elixir">$ mix ecto.reset
Generated hello app
The database for Hello.Repo has been dropped
The database for Hello.Repo has been created

14:38:37.418 [info]  Migrations already up</pre>
<p>Now we're ready to create our accounts context. We'll use <a href="mix.tasks.phx.gen.html"><code class="inline">mix phx.gen.html</code></a> which creates a context module that wraps up Ecto access for creating, updating, and deleting users, along with web files like controllers and templates for the web interface into our context. Run the following command at your project root:</p>
<pre data-language="elixir">$ mix phx.gen.html Accounts User users name:string \
username:string:unique

* creating lib/hello_web/controllers/user_controller.ex
* creating lib/hello_web/templates/user/edit.html.eex
* creating lib/hello_web/templates/user/form.html.eex
* creating lib/hello_web/templates/user/index.html.eex
* creating lib/hello_web/templates/user/new.html.eex
* creating lib/hello_web/templates/user/show.html.eex
* creating lib/hello_web/views/user_view.ex
* creating test/hello_web/controllers/user_controller_test.exs
* creating lib/hello/accounts/user.ex
* creating priv/repo/migrations/20170629175236_create_users.exs
* creating lib/hello/accounts.ex
* injecting lib/hello/accounts.ex
* creating test/hello/accounts_test.exs
* injecting test/hello/accounts_test.exs

Add the resource to your browser scope in lib/hello_web/router.ex:

    resources "/users", UserController


Remember to update your repository by running migrations:

    $ mix ecto.migrate
</pre>
<p>Phoenix generated the web files as expected in <code class="inline">lib/hello_web/</code>. We can also see our context files were generated inside a <code class="inline">lib/hello/accounts.ex</code> file and our user schema in the directory of the same name. Note the difference between <code class="inline">lib/hello</code> and <code class="inline">lib/hello_web</code>. We have an <code class="inline">Accounts</code> module to serve as the public API for account functionality, as well as an <code class="inline">Accounts.User</code> struct, which is an Ecto schema for casting and validating user account data. Phoenix also provided web and context tests for us, which we'll look at later. For now, let's follow the instructions and add the route according to the console instructions, in <code class="inline">lib/hello_web/router.ex</code>:</p>
<pre data-language="elixir">  scope "/", HelloWeb do
    pipe_through :browser

    get "/", PageController, :index
+   resources "/users", UserController
  end</pre>
<p>With the new route in place, Phoenix reminds us to update our repo by running <code class="inline">mix ecto.migrate</code>. Let's do that now:</p>
<pre data-language="elixir">$ mix ecto.migrate

[info]  == Running Hello.Repo.Migrations.CreateUsers.change/0 forward

[info]  create table users

[info]  create index users_username_index

[info]  == Migrated in 0.0s</pre>
<p>Before we jump into the generated code, let's start the server with <a href="mix.tasks.phx.server"><code class="inline">mix phx.server</code></a> and visit <a href="http://localhost:4000/users">http://localhost:4000/users</a>. Let's follow the "New User" link and click the "Submit" button without providing any input. We should be greeted with the following output:</p>
<pre data-language="elixir">Oops, something went wrong! Please check the errors below.</pre>
<p>When we submit the form, we can see all the validation errors inline with the inputs. Nice! Out of the box, the context generator included the schema fields in our form template and we can see our default validations for required inputs are in effect. Let's enter some example user data and resubmit the form:</p>
<pre data-language="elixir">User created successfully.

Show User
Name: Chris McCord
Username: chrismccord</pre>
<p>If we follow the "Back" link, we get a list of all users, which should contain the one we just created. Likewise, we can update this record or delete it. Now that we've seen how it works in the browser, it's time to take a look at the generated code.</p>
<h2 id="starting-with-generators" class="section-heading">  Starting With Generators </h2> <p>That little <a href="mix.tasks.phx.gen.html"><code class="inline">mix phx.gen.html</code></a> command packed a surprising punch. We got a lot of functionality out-of-the-box for creating, updating, and deleting users. This is far from a full-featured app, but remember, generators are first and foremost learning tools and a starting point for you to begin building real features. Code generation can't solve all your problems, but it will teach you the ins and outs of Phoenix and nudge you towards the proper mind-set when designing your application.</p>
<p>Let's first check out the <code class="inline">UserController</code> that was generated in <code class="inline">lib/hello_web/controllers/user_controller.ex</code>:</p>
<pre data-language="elixir">defmodule HelloWeb.UserController do
  use HelloWeb, :controller

  alias Hello.Accounts
  alias Hello.Accounts.User

  def index(conn, _params) do
    users = Accounts.list_users()
    render(conn, "index.html", users: users)
  end

  def new(conn, _params) do
    changeset = Accounts.change_user(%User{})
    render(conn, "new.html", changeset: changeset)
  end

  def create(conn, %{"user" =&gt; user_params}) do
    case Accounts.create_user(user_params) do
      {:ok, user} -&gt;
        conn
        |&gt; put_flash(:info, "User created successfully.")
        |&gt; redirect(to: Routes.user_path(conn, :show, user))
      {:error, %Ecto.Changeset{} = changeset} -&gt;
        render(conn, "new.html", changeset: changeset)
    end
  end
  ...
end</pre>
<p>We've seen how controllers work in our <a href="controllers">controller guide</a>, so the code probably isn't too surprising. What is worth noticing is how our controller calls into the <code class="inline">Accounts</code> context. We can see that the <code class="inline">index</code> action fetches a list of users with <code class="inline">Accounts.list_users/0</code>, and how users are persisted in the <code class="inline">create</code> action with <code class="inline">Accounts.create_user/1</code>. We haven't yet looked at the accounts context, so we don't yet know how user fetching and creation is happening under the hood â€“ <em>but that's the point</em>. Our Phoenix controller is the web interface into our greater application. It shouldn't be concerned with the details of how users are fetched from the database or persisted into storage. We only care about telling our application to perform some work for us. This is great because our business logic and storage details are decoupled from the web layer of our application. If we move to a full-text storage engine later for fetching users instead of a SQL query, our controller doesn't need to be changed. Likewise, we can reuse our context code from any other interface in our application, be it a channel, mix task, or long-running process importing CSV data.</p>
<p>In the case of our <code class="inline">create</code> action, when we successfully create a user, we use <a href="phoenix.controller#put_flash/3"><code class="inline">Phoenix.Controller.put_flash/3</code></a> to show a success message, and then we redirect to the <code class="inline">user_path</code>'s show page. Conversely, if <code class="inline">Accounts.create_user/1</code> fails, we render our <code class="inline">"new.html"</code> template and pass along the Ecto changeset for the template to lift error messages from.</p>
<p>Next, let's dig deeper and check out our <code class="inline">Accounts</code> context in <code class="inline">lib/hello/accounts.ex</code>:</p>
<pre data-language="elixir">defmodule Hello.Accounts do
  @moduledoc """
  The Accounts context.
  """

  import Ecto.Query, warn: false
  alias Hello.Repo

  alias Hello.Accounts.User

  @doc """
  Returns the list of users.

  ## Examples

      iex&gt; list_users()
      [%User{}, ...]

  """
  def list_users do
    Repo.all(User)
  end
  ...
end</pre>
<p>This module will be the public API for all account functionality in our system. For example, in addition to user account management, we may also handle user login credentials, account preferences, and password reset generation. If we look at the <code class="inline">list_users/0</code> function, we can see the private details of user fetching. And it's super simple. We have a call to <code class="inline">Repo.all(User)</code>. We saw how Ecto repo queries worked in <a href="ecto">the Ecto guide</a>, so this call should look familiar. Our <code class="inline">list_users</code> function is a generalized function specifying the <em>intent</em> of our code â€“ namely to list users. The details of that intent where we use our Repo to fetch the users from our PostgreSQL database is hidden from our callers. This is a common theme we'll see re-iterated as we use the Phoenix generators. Phoenix will push us to think about where we have different responsibilities in our application, and then to wrap up those different areas behind well-named modules and functions that make the intent of our code clear, while encapsulating the details.</p>
<p>Now we know how data is fetched, but how are users persisted? Let's take a look at the <code class="inline">Accounts.create_user/1</code> function:</p>
<pre data-language="elixir">  @doc """
  Creates a user.

  ## Examples

      iex&gt; create_user(%{field: value})
      {:ok, %User{}}

      iex&gt; create_user(%{field: bad_value})
      {:error, %Ecto.Changeset{}}

  """
  def create_user(attrs \\ %{}) do
    %User{}
    |&gt; User.changeset(attrs)
    |&gt; Repo.insert()
  end</pre>
<p>There's more documentation than code here, but a couple of things are important to highlight. First, we can see again that our Ecto Repo is used under the hood for database access. You probably also noticed the call to <code class="inline">User.changeset/2</code>. We talked about changesets before, and now we see them in action in our context.</p>
<p>If we open up the <code class="inline">User</code> schema in <code class="inline">lib/hello/accounts/user.ex</code>, it will look immediately familiar:</p>
<pre data-language="elixir">defmodule Hello.Accounts.User do
  use Ecto.Schema
  import Ecto.Changeset
  alias Hello.Accounts.User


  schema "users" do
    field :name, :string
    field :username, :string

    timestamps()
  end

  @doc false
  def changeset(%User{} = user, attrs) do
    user
    |&gt; cast(attrs, [:name, :username])
    |&gt; validate_required([:name, :username])
    |&gt; unique_constraint(:username)
  end
end</pre>
<p>This is just what we saw before when we ran <a href="mix.tasks.phx.gen.schema"><code class="inline">mix phx.gen.schema</code></a>, except here we see a <code class="inline">@doc false</code> above our <code class="inline">changeset/2</code> function. This tells us that while this function is publicly callable, it's not part of the public context API. Callers that build changesets do so via the context API. For example, <code class="inline">Accounts.create_user/1</code> calls into our <code class="inline">User.changeset/2</code> to build the changeset from user input. Callers, such as our controller actions, do not access <code class="inline">User.changeset/2</code> directly. All interaction with our user changesets is done through the public <code class="inline">Accounts</code> context.</p>
<h2 id="in-context-relationships" class="section-heading">  In-context Relationships </h2> <p>Our basic user account features are nice, but let's take it up a notch by supporting user login credentials. We won't implement a complete authentication system, but we'll give ourselves a good start to grow such a system from. Many authentication solutions couple the user credentials to an account in a one-to-one fashion, but this often causes issues. For example, supporting different login methods, such as social login or recovery email addresses, will cause major code changes. Let's set up a credentials association that will allow us to start off tracking a single credential per account, but easily support more features later.</p>
<p>For now, user credentials will contain only email information. Our first order of business is to decide where credentials live in the application. We have our <code class="inline">Accounts</code> context, which manages user accounts. User credentials is a natural fit here. Phoenix is also smart enough to generate code inside an existing context, which makes adding new resources to a context a breeze. Run the following command at your project root:</p>
<blockquote><p>Sometimes it may be tricky to determine if two resources belong to the same context or not. In those cases, prefer distinct contexts per resource and refactor later if necessary. Otherwise you can easily end-up with large contexts of loosely related entities. Also keep in mind that the fact two resources are related does not necessarily mean they belong to the same context, otherwise you would quickly end-up with one large context, as the majority of resources in an application are connected to each other. To sum it up: if you are unsure, you should prefer explicit modules (contexts) between resources.</p></blockquote>
<pre data-language="elixir">$ mix phx.gen.context Accounts Credential credentials \
email:string:unique user_id:references:users

* creating lib/hello/accounts/credential.ex
* creating priv/repo/migrations/20170629180555_create_credentials.exs
* injecting lib/hello/accounts.ex
* injecting test/hello/accounts_test.exs

Remember to update your repository by running migrations:

    $ mix ecto.migrate</pre>
<p>This time around, we used <a href="mix.tasks.phx.gen.context"><code class="inline">mix phx.gen.context</code></a>, which is just like <a href="mix.tasks.phx.gen.html"><code class="inline">mix phx.gen.html</code></a>, except it doesn't generate the web files for us. Since we already have controllers and templates for managing users, we can integrate the new credential features into our existing web form.</p>
<p>We can see from the output that Phoenix generated an <code class="inline">accounts/credential.ex</code> file for our <code class="inline">Accounts.Credential</code> schema, as well as a migration. Notably, phoenix said it was <code class="inline">* injecting</code> code into the existing <code class="inline">accounts.ex</code> context file and test file. Since our <code class="inline">Accounts</code> module already exists, Phoenix knows to inject our code here.</p>
<p>Before we run our migrations, we need to make one change to the generated migration to enforce data integrity of user account credentials. In our case, we want a user's credentials to be deleted when the parent user is removed. Make the following change to your <code class="inline">*_create_credentials.exs</code> migration file in <code class="inline">priv/repo/migrations/</code>:</p>
<pre data-language="elixir">  def change do
    create table(:credentials) do
      add :email, :string
-     add :user_id, references(:users, on_delete: :nothing)
+     add :user_id, references(:users, on_delete: :delete_all),
+                   null: false

      timestamps()
    end

    create unique_index(:credentials, [:email])
    create index(:credentials, [:user_id])
  end</pre>
<p>We changed the <code class="inline">:on_delete</code> option from <code class="inline">:nothing</code> to <code class="inline">:delete_all</code>, which will generate a foreign key constraint that will delete all credentials for a given user when the user is removed from the database. Likewise, we also passed <code class="inline">null: false</code> to disallow creating credentials without an existing user. By using a database constraint, we enforce data integrity at the database level, rather than relying on ad-hoc and error-prone application logic.</p>
<p>Next, let's migrate up our database as Phoenix instructed:</p>
<pre data-language="elixir">$ mix ecto.migrate
mix ecto.migrate
Compiling 2 files (.ex)
Generated hello app

[info]  == Running Hello.Repo.Migrations.CreateCredentials.change/0 forward

[info]  create table credentials

[info]  create index credentials_email_index

[info]  create index credentials_user_id_index

[info]  == Migrated in 0.0s</pre>
<p>Before we integrate credentials in the web layer, we need to let our context know how to associate users and credentials. First, open up <code class="inline">lib/hello/accounts/user.ex</code> and add the following association:</p>
<pre data-language="elixir">+ alias Hello.Accounts.Credential


  schema "users" do
    field :name, :string
    field :username, :string
+   has_one :credential, Credential

    timestamps()
  end

</pre>
<p>We used <code class="inline">Ecto.Schema</code>'s <code class="inline">has_one</code> macro to let Ecto know how to associate our parent User to a child Credential. Next, let's add the relationships in the opposite direction in <code class="inline">accounts/credential.ex</code>:</p>
<pre data-language="elixir">+ alias Hello.Accounts.User


  schema "credentials" do
    field :email, :string
-   field :user_id, :id
+   belongs_to :user, User

    timestamps()
  end
</pre>
<p>We used the <code class="inline">belongs_to</code> macro to map our child relationship to the parent <code class="inline">User</code>. With our schema associations set up, let's open up <code class="inline">accounts.ex</code> and make the following changes to the generated <code class="inline">list_users</code> and <code class="inline">get_user!</code> functions:</p>
<pre data-language="elixir">  def list_users do
    User
    |&gt; Repo.all()
    |&gt; Repo.preload(:credential)
  end

  def get_user!(id) do
    User
    |&gt; Repo.get!(id)
    |&gt; Repo.preload(:credential)
  end</pre>
<p>We rewrote the <code class="inline">list_users/0</code> and <code class="inline">get_user!/1</code> to preload the credential association whenever we fetch users. The Repo preload functionality fetches a schema's association data from the database, then places it inside the schema. When operating on a collection, such as our query in <code class="inline">list_users</code>, Ecto can efficiently preload the associations in a single query. This allows us to represent our <code class="inline">%Accounts.User{}</code> structs as always containing credentials without the caller having to worry about fetching the extra data.</p>
<p>Next, let's expose our new feature to the web by adding the credentials input to our user form. Open up <code class="inline">lib/hello_web/templates/user/form.html.eex</code> and key in the new credential form group above the submit button:</p>
<pre data-language="elixir">  ...
+ &lt;div class="form-group"&gt;
+   &lt;%= inputs_for f, :credential, fn cf -&gt; %&gt;
+     &lt;%= label cf, :email %&gt;
+     &lt;%= text_input cf, :email %&gt;
+     &lt;%= error_tag cf, :email %&gt;
+   &lt;% end %&gt;
+ &lt;/div&gt;

  &lt;%= submit "Submit" %&gt;</pre>
<p>We used <a href="../../phoenix_html/phoenix.html"><code class="inline">Phoenix.HTML</code></a>'s <code class="inline">inputs_for</code> function to add an associations nested fields within the parent form. Within the nested inputs, we rendered our credential's email field and included the <code class="inline">label</code> and <code class="inline">error_tag</code> helpers just like our other inputs.</p>
<p>Next, let's show the user's email address in the user show template. Add the following code to <code class="inline">lib/hello_web/templates/user/show.html.eex</code>:</p>
<pre data-language="elixir">  ...
+ &lt;li&gt;
+   &lt;strong&gt;Email:&lt;/strong&gt;
+   &lt;%= @user.credential.email %&gt;
+ &lt;/li&gt;
&lt;/ul&gt;
</pre>
<p>Now if we visit <a href="http://localhost:4000/users/new">http://localhost:4000/users/new</a>, we'll see the new email input, but if you try to save a user, you'll find that the email field is ignored. No validations are run telling you it was blank and the data was not saved, and at the end you'll get an exception <code class="inline">(UndefinedFunctionError) function nil.email/0 is undefined or private</code>. What gives?</p>
<p>We used Ecto's <code class="inline">belongs_to</code> and <code class="inline">has_one</code> associations to wire-up how our data is related at the context level, but remember this is decoupled from our web-facing user input. To associate user input to our schema associations, we need to handle it the way we've handled other user input so far â€“ in changesets. Remove the alias for Credential added by the generator and modify your <code class="inline">alias Hello.Accounts.User</code>, <code class="inline">create_user/1</code> and <code class="inline">update_user/2</code> functions in your <code class="inline">Accounts</code> context to build a changeset which knows how to cast user input with nested credential information:</p>
<pre data-language="elixir">- alias Hello.Accounts.User
+ alias Hello.Accounts.{User, Credential}
  ...

  def update_user(%User{} = user, attrs) do
    user
    |&gt; User.changeset(attrs)
+   |&gt; Ecto.Changeset.cast_assoc(:credential, with: &amp;Credential.changeset/2)
    |&gt; Repo.update()
  end

  def create_user(attrs \\ %{}) do
    %User{}
    |&gt; User.changeset(attrs)
+   |&gt; Ecto.Changeset.cast_assoc(:credential, with: &amp;Credential.changeset/2)
    |&gt; Repo.insert()
  end
  ...

- alias Hello.Accounts.Credential</pre>
<p>We updated the functions to pipe our user changeset into <code class="inline">Ecto.Changeset.cast_assoc/3</code>. Ecto's <code class="inline">cast_assoc/3</code> allows us to tell the changeset how to cast user input to a schema relation. We also used the <code class="inline">:with</code> option to tell Ecto to use our <code class="inline">Credential.changeset/2</code> function to cast the data. This way, any validations we perform in <code class="inline">Credential.changeset/2</code> will be applied when saving the <code class="inline">User</code> changeset.</p>
<p>Finally, if you visit <a href="http://localhost:4000/users/new">http://localhost:4000/users/new</a> and attempt to save an empty email address, you'll see the proper validation error message. If you enter valid information, the data will be casted and persisted properly.</p>
<pre data-language="elixir">Show User
Name: Chris McCord
Username: chrismccord
Email: chris@example.com</pre>
<p>It's not much to look at yet, but it works! We added relationships within our context complete with data integrity enforced by the database. Not bad. Let's keep building!</p>
<h2 id="adding-account-functions" class="section-heading">  Adding Account functions </h2> <p>As we've seen, your context modules are dedicated modules that expose and group related functionality. Phoenix generates generic functions, such as <code class="inline">list_users</code> and <code class="inline">update_user</code>, but they only serve as a basis for you to grow your business logic and application from. To begin extending our <code class="inline">Accounts</code> context with real features, let's address an obvious issue of our application â€“ we can create users with credentials in our system, but they have no way of signing in with those credentials. Building a complete user authentication system is beyond the scope of this guide, but let's get started with a basic email-only sign-in page that allows us to track a current user's session. This will let us focus on extending our <code class="inline">Accounts</code> context while giving you a good start to grow a complete authentication solution from.</p>
<p>To start, let's think of a function name that describes what we want to accomplish. To authenticate a user by email address, we'll need a way to lookup that user and verify their entered credentials are valid. We can do this by exposing a single function on our <code class="inline">Accounts</code> context.</p>
<pre data-language="elixir">&gt; user = Accounts.authenticate_by_email_password(email, password)</pre>
<p>That looks nice. A descriptive name that exposes the intent of our code is best. This function makes it crystal clear what purpose it serves, while allowing our caller to remain blissfully unaware of the internal details. Make the following additions to your <code class="inline">lib/hello/accounts.ex</code> file:</p>
<pre data-language="elixir">def authenticate_by_email_password(email, _password) do
  query =
    from u in User,
      inner_join: c in assoc(u, :credential),
      where: c.email == ^email

  case Repo.one(query) do
    %User{} = user -&gt; {:ok, user}
    nil -&gt; {:error, :unauthorized}
  end
end</pre>
<p>We defined an <code class="inline">authenticate_by_email_password/2</code> function, which discards the password field for now, but you could integrate tools like <a href="https://github.com/ueberauth/guardian">guardian</a> or <a href="https://github.com/riverrun/comeonin">comeonin</a> as you continue building your application. All we need to do in our function is find the user with matching credentials and return the <code class="inline">%Accounts.User{}</code> struct in a <code class="inline">:ok</code> tuple, or an <code class="inline">{:error, :unauthorized}</code> value to let the caller know their authentication attempt has failed.</p>
<p>Now that we can authenticate a user from our context, let's add a login page to our web layer. First create a new controller in <code class="inline">lib/hello_web/controllers/session_controller.ex</code>:</p>
<pre data-language="elixir">defmodule HelloWeb.SessionController do
  use HelloWeb, :controller

  alias Hello.Accounts

  def new(conn, _) do
    render(conn, "new.html")
  end

  def create(conn, %{"user" =&gt; %{"email" =&gt; email, "password" =&gt; password}}) do
    case Accounts.authenticate_by_email_password(email, password) do
      {:ok, user} -&gt;
        conn
        |&gt; put_flash(:info, "Welcome back!")
        |&gt; put_session(:user_id, user.id)
        |&gt; configure_session(renew: true)
        |&gt; redirect(to: "/")
      {:error, :unauthorized} -&gt;
        conn
        |&gt; put_flash(:error, "Bad email/password combination")
        |&gt; redirect(to: Routes.session_path(conn, :new))
    end
  end

  def delete(conn, _) do
    conn
    |&gt; configure_session(drop: true)
    |&gt; redirect(to: "/")
  end
end</pre>
<p>We defined a <code class="inline">SessionController</code> to handle users signing in and out of the application. Our <code class="inline">new</code> action is responsible for simply rendering a "new session" form, which posts out to the create action of our controller. In <code class="inline">create</code>, we pattern match the form fields and call into our <code class="inline">Accounts.authenticate_by_email_password/2</code> that we just defined. If successful, we use <a href="../../plug/plug.conn#put_session/3"><code class="inline">Plug.Conn.put_session/3</code></a> to place the authenticated user's ID in the session, and redirect to the home page with a successful welcome message. We also called <code class="inline">configure_session(conn, renew: true)</code> before redirecting to avoid <a href="https://owasp.org/www-community/attacks/Session_fixation">session fixation attacks</a>. If authentication fails, we add a flash error message, and redirect to the sign-in page for the user to try again. To finish the controller, we support a <code class="inline">delete</code> action which simply calls <a href="../../plug/plug.conn#configure_session/2"><code class="inline">Plug.Conn.configure_session/2</code></a> to drop the session and redirect to the home page.</p>
<p>Next, let's wire up our session routes in <code class="inline">lib/hello_web/router.ex</code>:</p>
<pre data-language="elixir">  scope "/", HelloWeb do
    pipe_through :browser

    get "/", PageController, :index
    resources "/users", UserController
+   resources "/sessions", SessionController, only: [:new, :create, :delete],
                                              singleton: true
  end</pre>
<p>We used <code class="inline">resources</code> to generate a set of routes under the <code class="inline">"/session"</code> path. This is what we've done for other routes, except this time we also passed the <code class="inline">:only</code> option to limit which routes are generated, since we only need to support <code class="inline">:new</code>, <code class="inline">:create</code>, and <code class="inline">:delete</code> actions. We also used the <code class="inline">singleton: true</code> option, which defines all the RESTful routes, but does not require a resource ID to be passed along in the URL. We don't need an ID in the URL because our actions are always scoped to the "current" user in the system. The ID is always in the session. Before we finish our router, let's add an authentication plug to the router that will allow us to lock down certain routes after a user has used our new session controller to sign-in. Add the following function to <code class="inline">lib/hello_web/router.ex</code>:</p>
<pre data-language="elixir">  defp authenticate_user(conn, _) do
    case get_session(conn, :user_id) do
      nil -&gt;
        conn
        |&gt; Phoenix.Controller.put_flash(:error, "Login required")
        |&gt; Phoenix.Controller.redirect(to: "/")
        |&gt; halt()
      user_id -&gt;
        assign(conn, :current_user, Hello.Accounts.get_user!(user_id))
    end
  end</pre>
<p>We defined an <code class="inline">authenticate_user/2</code> plug in the router which simply uses <a href="../../plug/plug.conn#get_session/2"><code class="inline">Plug.Conn.get_session/2</code></a> to check for a <code class="inline">:user_id</code> in the session. If we find one, it means a user has previously authenticated, and we call into <code class="inline">Hello.Accounts.get_user!/1</code> to place our <code class="inline">:current_user</code> into the connection assigns. If we don't have a session, we add a flash error message, redirect to the homepage, and we use <a href="../../plug/plug.conn#halt/1"><code class="inline">Plug.Conn.halt/1</code></a> to halt further plugs downstream from being invoked. We won't use this new plug quite yet, but it will be ready and waiting as we add authenticated routes in just a moment.</p>
<p>Lastly, we need <code class="inline">SessionView</code> to render a template for our login form. Create a new file in <code class="inline">lib/hello_web/views/session_view.ex</code>:</p>
<pre data-language="elixir">defmodule HelloWeb.SessionView do
  use HelloWeb, :view
end</pre>
<p>Next, add a new template in <code class="inline">lib/hello_web/templates/session/new.html.eex</code>:</p>
<pre data-language="elixir">&lt;h1&gt;Sign in&lt;/h1&gt;

&lt;%= form_for @conn, Routes.session_path(@conn, :create), [method: :post, as: :user], fn f -&gt; %&gt;
  &lt;div class="form-group"&gt;
    &lt;%= text_input f, :email, placeholder: "Email" %&gt;
  &lt;/div&gt;

  &lt;div class="form-group"&gt;
    &lt;%= password_input f, :password, placeholder: "Password" %&gt;
  &lt;/div&gt;

  &lt;div class="form-group"&gt;
    &lt;%= submit "Login" %&gt;
  &lt;/div&gt;
&lt;% end %&gt;

&lt;%= form_for @conn, Routes.session_path(@conn, :delete), [method: :delete, as: :user], fn _ -&gt; %&gt;
  &lt;div class="form-group"&gt;
    &lt;%= submit "logout" %&gt;
  &lt;/div&gt;
&lt;% end %&gt;</pre>
<p>To keep things simple, we added both our sign-in and sign-out forms in this template. For our sign-in form, we pass the <code class="inline">@conn</code> directly to <code class="inline">form_for</code>, pointing our form action at <code class="inline">session_path(@conn, :create)</code>. We also pass the <code class="inline">as: :user</code> option which tells Phoenix to wrap the form parameters inside a <code class="inline">"user"</code> key. Next, we used the <code class="inline">text_input</code> and <code class="inline">password_input</code> functions to send up an <code class="inline">"email"</code> and <code class="inline">"password"</code> parameter.</p>
<p>For logging out, we simply defined a form that sends the <code class="inline">DELETE</code> HTTP method to server's session delete path. Now if you visit the sign-in page at <a href="http://localhost:4000/sessions/new">http://localhost:4000/sessions/new</a> and enter a bad email address, you should be greeted with your flash message. Entering a valid email address will redirect to the home page with a success flash notice.</p>
<p>With authentication in place, we're in good shape to begin building out our next features.</p>
<h2 id="cross-context-dependencies" class="section-heading">  Cross-context dependencies </h2> <p>Now that we have the beginnings of user account and credential features, let's begin to work on the other main features of our application â€“ managing page content. We want to support a content management system (CMS) where authors can create and edit pages of the site. While we could extend our <code class="inline">Accounts</code> context with CMS features, if we step back and think about the isolation of our application, we can see it doesn't fit. An accounts system shouldn't care at all about a CMS system. The responsibilities of our <code class="inline">Accounts</code> context is to manage users and their credentials, not handle page content changes. There's a clear need here for a separate context to handle these responsibilities. Let's call it <code class="inline">CMS</code>.</p>
<p>Let's create a <code class="inline">CMS</code> context to handle basic CMS duties. Before we write code, let's imagine we have the following CMS feature requirements:</p>
<ol>
<li>Page creation and updates</li>
<li>Pages belong to Authors who are responsible for publishing changes</li>
<li>Author information should appear with the page, and include information such as author bio and role within the CMS, such as <code class="inline">"editor"</code>, <code class="inline">"writer"</code>, or <code class="inline">"intern"</code>.</li>
</ol>
<p>From the description, it's clear we need a <code class="inline">Page</code> resource for storing page information. What about our author information? While we could extend our existing <code class="inline">Accounts.User</code> schema to include information such as bio and role, that would violate the responsibilities we've set up for our contexts. Why should our Account system now be aware of author information? Worse, with a field like "role", the CMS role in the system will likely conflict or be confused with other account roles for our application. There's a better way.</p>
<p>Applications with "users" are naturally heavily user driven. After all, our software is typically designed to be used by human end-users one way or another. Instead of extending our <code class="inline">Accounts.User</code> struct to track every field and responsibility of our entire platform, it's better to keep those responsibilities with the modules who own that functionality. In our case, we can create a <code class="inline">CMS.Author</code> struct that holds author specific fields as it relates to a CMS. Now we can place fields like "role" and "bio" here, where they naturally live. Likewise, we also gain specialized datastructures in our application that are suited to the domain that we are operating in, rather than a single <code class="inline">%User{}</code> in the system that has to be everything to everyone.</p>
<p>With our plan set, let's get to work. Run the following command to generate our new context:</p>
<pre data-language="elixir">$ mix phx.gen.html CMS Page pages title:string body:text \
views:integer --web CMS

* creating lib/hello_web/controllers/cms/page_controller.ex
* creating lib/hello_web/templates/cms/page/edit.html.eex
* creating lib/hello_web/templates/cms/page/form.html.eex
* creating lib/hello_web/templates/cms/page/index.html.eex
* creating lib/hello_web/templates/cms/page/new.html.eex
* creating lib/hello_web/templates/cms/page/show.html.eex
* creating lib/hello_web/views/cms/page_view.ex
* creating test/hello_web/controllers/cms/page_controller_test.exs
* creating lib/hello/cms/page.ex
* creating priv/repo/migrations/20170629195946_create_pages.exs
* creating lib/hello/cms.ex
* injecting lib/hello/cms.ex
* creating test/hello/cms/cms_test.exs
* injecting test/hello/cms/cms_test.exs

Add the resource to your CMS :browser scope in lib/hello_web/router.ex:

    scope "/cms", HelloWeb.CMS, as: :cms do
      pipe_through :browser
      ...
      resources "/pages", PageController
    end


Remember to update your repository by running migrations:

    $ mix ecto.migrate
</pre>
<p>The <code class="inline">views</code> attribute on the pages will not be updated directly by the user, so let's remove it from the generated form. Open <code class="inline">lib/hello_web/templates/cms/page/form.html.eex</code> and remove this part:</p>
<pre data-language="elixir">-  &lt;%= label f, :views %&gt;
-  &lt;%= number_input f, :views %&gt;
-  &lt;%= error_tag f, :views %&gt;</pre>
<p>Also, change <code class="inline">lib/hello/cms/page.ex</code> to remove <code class="inline">:views</code> from the permitted params:</p>
<pre data-language="elixir">  def changeset(%Page{} = page, attrs) do
    page
-    |&gt; cast(attrs, [:title, :body, :views])
-    |&gt; validate_required([:title, :body, :views])
+    |&gt; cast(attrs, [:title, :body])
+    |&gt; validate_required([:title, :body])
  end</pre>
<p>Finally, open up the new file in <code class="inline">priv/repo/migrations</code> to ensure the <code class="inline">views</code> attribute will have a default value:</p>
<pre data-language="elixir">    create table(:pages) do
      add :title, :string
      add :body, :text
-     add :views, :integer
+     add :views, :integer, default: 0

      timestamps()
    end</pre>
<p>This time we passed the <code class="inline">--web</code> option to the generator. This tells Phoenix what namespace to use for the web modules, such as controllers and views. This is useful when you have conflicting resources in the system, such as our existing <code class="inline">PageController</code>, as well as a way to naturally namespace paths and functionality of different features, like a CMS system. Phoenix instructed us to add a new <code class="inline">scope</code> to the router for a <code class="inline">"/cms"</code> path prefix. Let's copy paste the following into our <code class="inline">lib/hello_web/router.ex</code>, but we'll make one modification to the <code class="inline">pipe_through</code> macro:</p>
<pre data-language="elixir">  scope "/cms", HelloWeb.CMS, as: :cms do
    pipe_through [:browser, :authenticate_user]

    resources "/pages", PageController
  end
</pre>
<p>We added the <code class="inline">:authenticate_user</code> plug to require a signed-in user for all routes within this CMS scope. With our routes in place, we can migrate up the database:</p>
<pre data-language="elixir">$ mix ecto.migrate

Compiling 12 files (.ex)
Generated hello app

[info]  == Running Hello.Repo.Migrations.CreatePages.change/0 forward

[info]  create table pages

[info]  == Migrated in 0.0s</pre>
<p>Now, let's fire up the server with <a href="mix.tasks.phx.server"><code class="inline">mix phx.server</code></a> and visit <a href="http://localhost:4000/cms/pages">http://localhost:4000/cms/pages</a>. If we haven't logged in yet, we'll be redirected to the home page with a flash error message telling us to sign in. Let's sign in at <a href="http://localhost:4000/sessions/new">http://localhost:4000/sessions/new</a>, then re-visit <a href="http://localhost:4000/cms/pages">http://localhost:4000/cms/pages</a>. Now that we're authenticated, we should see a familiar resource listing for pages, with a <code class="inline">New Page</code> link.</p>
<p>Before we create any pages, we need page authors. Let's run the <a href="mix.tasks.phx.gen.context"><code class="inline">mix phx.gen.context</code></a> generator to generate an <code class="inline">Author</code> schema along with injected context functions:</p>
<pre data-language="elixir">$ mix phx.gen.context CMS Author authors bio:text role:string \
genre:string user_id:references:users:unique

* creating lib/hello/cms/author.ex
* creating priv/repo/migrations/20170629200937_create_authors.exs
* injecting lib/hello/cms.ex
* injecting test/hello/cms/cms_test.exs

Remember to update your repository by running migrations:

    $ mix ecto.migrate
</pre>
<p>We used the context generator to inject code, just like when we generated our credentials code. We added fields for the author bio, their role in the content management system, the genre the author writes in, and lastly a foreign key to a user in our accounts system. Since our accounts context is still the authority on end-users in our application, we will depend on it for our CMS authors. That said, any information specific to authors will stay in the authors schema. We could also decorate our <code class="inline">Author</code> with user account information by using virtual fields and never expose the <code class="inline">User</code> structure. This would ensure consumers of the CMS API are protected from changes in the <code class="inline">User</code> context.</p>
<p>Before we migrate our database, we need to handle data integrity once again in the newly generated <code class="inline">*_create_authors.exs</code> migration. Open up the new file in <code class="inline">priv/repo/migrations</code> and make the following change to the foreign key constraint:</p>
<pre data-language="elixir">  def change do
    create table(:authors) do
      add :bio, :text
      add :role, :string
      add :genre, :string
-     add :user_id, references(:users, on_delete: :nothing)
+     add :user_id, references(:users, on_delete: :delete_all),
+                   null: false

      timestamps()
    end

    create unique_index(:authors, [:user_id])
  end</pre>
<p>We used the <code class="inline">:delete_all</code> strategy again to enforce data integrity. This way, when a user is deleted from the application through <code class="inline">Accounts.delete_user/1)</code>, we don't have to rely on application code in our <code class="inline">Accounts</code> context to worry about cleaning up the <code class="inline">CMS</code> author records. This keeps our application code decoupled and the data integrity enforcement where it belongs â€“ in the database.</p>
<p>Before we continue, we have a final migration to generate. Now that we have an authors table, we can associate pages and authors. Let's add an <code class="inline">author_id</code> field to the pages table. Run the following command to generate a new migration:</p>
<pre data-language="elixir">$ mix ecto.gen.migration add_author_id_to_pages

* creating priv/repo/migrations
* creating priv/repo/migrations/20170629202117_add_author_id_to_pages.exs</pre>
<p>Now open up the new <code class="inline">*_add_author_id_to_pages.exs</code> file in <code class="inline">priv/repo/migrations</code> and key this in:</p>
<pre data-language="elixir">  def change do
    alter table(:pages) do
      add :author_id, references(:authors, on_delete: :delete_all),
                      null: false
    end

    create index(:pages, [:author_id])
  end</pre>
<p>We used the <code class="inline">alter</code> macro to add a new <code class="inline">author_id</code> field to the pages table, with a foreign key to our authors table. We also used the <code class="inline">on_delete: :delete_all</code> option again to prune any pages when a parent author is deleted from the application.</p>
<p>Now let's migrate up:</p>
<pre data-language="elixir">$ mix ecto.migrate

[info]  == Running Hello.Repo.Migrations.CreateAuthors.change/0 forward

[info]  create table authors

[info]  create index authors_user_id_index

[info]  == Migrated in 0.0s

[info]  == Running Hello.Repo.Migrations.AddAuthorIdToPages.change/0 forward

[info]  == Migrated in 0.0s</pre>
<p>With our database ready, let's integrate authors and posts in the CMS system.</p>
<h2 id="cross-context-data" class="section-heading">  Cross-context data </h2> <p>Dependencies in your software are often unavoidable, but we can do our best to limit them where possible and lessen the maintenance burden when a dependency is necessary. So far, we've done a great job isolating the two main contexts of our application from each other, but now we have a necessary dependency to handle.</p>
<p>Our <code class="inline">Author</code> resource serves to keep the responsibilities of representing an author inside the CMS, but ultimately for an author to exist at all, an end-user represented by an <code class="inline">Accounts.User</code> must be present. Given this, our <code class="inline">CMS</code> context will have a data dependency on the <code class="inline">Accounts</code> context. With that in mind, we have two options. One is to expose APIs on the <code class="inline">Accounts</code> contexts that allows us to efficiently fetch user data for use in the CMS system, or we can use database joins to fetch the dependent data. Both are valid options given your tradeoffs and application size, but joining data from the database when you have a hard data dependency is just fine for a large class of applications. If you decide to break out coupled contexts into entirely separate applications and databases at a later time, you still gain the benefits of isolation. This is because your public context APIs will likely remain unchanged.</p>
<p>Now that we know where our data dependencies exist, let's add our schema associations so we can tie pages to authors and authors to users. Make the following changes to <code class="inline">lib/hello/cms/page.ex</code>:</p>
<pre data-language="elixir">+ alias Hello.CMS.Author


  schema "pages" do
    field :body, :string
    field :title, :string
    field :views, :integer
+   belongs_to :author, Author

    timestamps()
  end</pre>
<p>We added a <code class="inline">belongs_to</code> relationships between pages and their authors. Next, let's add the association in the other direction in <code class="inline">lib/hello/cms/author.ex</code>:</p>
<pre data-language="elixir">
+ alias Hello.CMS.Page


  schema "authors" do
    field :bio, :string
    field :genre, :string
    field :role, :string

-   field :user_id, :id
+   has_many :pages, Page
+   belongs_to :user, Hello.Accounts.User

    timestamps()
  end</pre>
<p>We added the <code class="inline">has_many</code> association for author pages, and then introduced our data dependency on the <code class="inline">Accounts</code> context by wiring up the <code class="inline">belongs_to</code> association to our <code class="inline">Accounts.User</code> schema.</p>
<p>With our associations in place, let's update our <code class="inline">CMS</code> context to require an author when creating or updating a page. We'll start off with data fetching changes. Open up your <code class="inline">CMS</code> context in <code class="inline">lib/hello/cms.ex</code> and replace the <code class="inline">list_pages/0</code>, <code class="inline">get_page!/1</code>, and <code class="inline">get_author!/1</code> functions with the following definitions:</p>
<pre data-language="elixir">  alias Hello.CMS.{Page, Author}
  alias Hello.Accounts

  def list_pages do
    Page
    |&gt; Repo.all()
    |&gt; Repo.preload(author: [user: :credential])
  end

  def get_page!(id) do
    Page
    |&gt; Repo.get!(id)
    |&gt; Repo.preload(author: [user: :credential])
  end

  def get_author!(id) do
    Author
    |&gt; Repo.get!(id)
    |&gt; Repo.preload(user: :credential)
  end</pre>
<p>We started by rewriting the <code class="inline">list_pages/0</code> function to preload the associated author, user, and credential data from the database. Next, we rewrote <code class="inline">get_page!/1</code> and <code class="inline">get_author!/1</code> to also preload the necessary data.</p>
<p>With our data access functions in place, let's turn our focus towards persistence. We can fetch authors alongside pages, but we haven't yet allowed authors to be persisted when we create or edit pages. Let's fix that. Open up <code class="inline">lib/hello/cms.ex</code> and make the following changes:</p>
<pre data-language="elixir">def create_page(%Author{} = author, attrs \\ %{}) do
  %Page{}
  |&gt; Page.changeset(attrs)
  |&gt; Ecto.Changeset.put_change(:author_id, author.id)
  |&gt; Repo.insert()
end

def ensure_author_exists(%Accounts.User{} = user) do
  %Author{user_id: user.id}
  |&gt; Ecto.Changeset.change()
  |&gt; Ecto.Changeset.unique_constraint(:user_id)
  |&gt; Repo.insert()
  |&gt; handle_existing_author()
end
defp handle_existing_author({:ok, author}), do: author
defp handle_existing_author({:error, changeset}) do
  Repo.get_by!(Author, user_id: changeset.data.user_id)
end</pre>
<p>There's a bit of a code here, so let's break it down. First, we rewrote the <code class="inline">create_page</code> function to require a <code class="inline">CMS.Author</code> struct, which represents the author publishing the post. We then take our changeset and pass it to <code class="inline">Ecto.Changeset.put_change/2</code> to place the <code class="inline">author_id</code> association in the changeset. Next, we use <code class="inline">Repo.insert</code> to insert the new page into the database, containing our associated <code class="inline">author_id</code>.</p>
<p>Our CMS system requires an author to exist for any end-user before they publish posts, so we added an <code class="inline">ensure_author_exists</code> function to programmatically allow authors to be created. Our new function accepts an <code class="inline">Accounts.User</code> struct and either finds the existing author in the application with that <code class="inline">user.id</code>, or creates a new author for the user. Our authors table has a unique constraint on the <code class="inline">user_id</code> foreign key, so we are protected from a race condition allowing duplicate authors. That said, we still need to protect ourselves from racing the insert of another user. To accomplish this, we use a purpose-built changeset with <code class="inline">Ecto.Changeset.change/1</code> which accepts a new <code class="inline">Author</code> struct with our <code class="inline">user_id</code>. The changeset's only purpose is to convert a unique constraint violation into an error we can handle. After attempting to insert the new author with <code class="inline">Repo.insert/1</code>, we pipe to <code class="inline">handle_existing_author/1</code> which matches on the success and error cases. For the success case, we are done and simply return the created author, otherwise we use <code class="inline">Repo.get_by!</code> to fetch the author for the <code class="inline">user_id</code> that already exists.</p>
<p>That wraps up our <code class="inline">CMS</code> changes. Now, let's update our web layer to support our additions. Before we update our individual CMS controller actions, we need to make a couple of additions to the <code class="inline">CMS.PageController</code> plug pipeline. First, we must ensure an author exists for end-users accessing the CMS, and we need to authorize access to page owners.</p>
<p>Open up your generated <code class="inline">lib/hello_web/controllers/cms/page_controller.ex</code> and make the following additions:</p>
<pre data-language="elixir">
  plug :require_existing_author
  plug :authorize_page when action in [:edit, :update, :delete]

  ...

  defp require_existing_author(conn, _) do
    author = CMS.ensure_author_exists(conn.assigns.current_user)
    assign(conn, :current_author, author)
  end

  defp authorize_page(conn, _) do
    page = CMS.get_page!(conn.params["id"])

    if conn.assigns.current_author.id == page.author_id do
      assign(conn, :page, page)
    else
      conn
      |&gt; put_flash(:error, "You can't modify that page")
      |&gt; redirect(to: Routes.cms_page_path(conn, :index))
      |&gt; halt()
    end
  end</pre>
<p>We added two new plugs to our <code class="inline">CMS.PageController</code>. The first plug, <code class="inline">:require_existing_author</code>, runs for every action in this controller. The <code class="inline">require_existing_author/2</code> plug calls into our <code class="inline">CMS.ensure_author_exists/1</code> and passes in the <code class="inline">current_user</code> from the connection assigns. After finding or creating the author, we use <a href="../../plug/plug.conn#assign/3"><code class="inline">Plug.Conn.assign/3</code></a> to place a <code class="inline">:current_author</code> key into the assigns for use downstream.</p>
<p>Next, we added an <code class="inline">:authorize_page</code> plug that makes use of plug's guard clause feature where we can limit the plug to only certain actions. The definition for our <code class="inline">authorize_page/2</code> plug first fetches the page from the connection params, then does an authorization check against the <code class="inline">current_author</code>. If our current author's ID matches the fetched page ID, we have verified the page's owner is accessing the page and we simply assign the <code class="inline">page</code> into the connection assigns to be used in the controller action. If our authorization fails, we add a flash error message, redirect to the page index screen, and then call <a href="../../plug/plug.conn#halt/1"><code class="inline">Plug.Conn.halt/1</code></a> to prevent the plug pipeline from continuing and invoking the controller action.</p>
<p>With our new plugs in place, we can now modify our <code class="inline">create</code>, <code class="inline">edit</code>, <code class="inline">update</code>, and <code class="inline">delete</code> actions to make use of the new values in the connection assigns:</p>
<pre data-language="elixir">- def edit(conn, %{"id" =&gt; id}) do
+ def edit(conn, _) do
-   page = CMS.get_page!(id)
-   changeset = CMS.change_page(page)
+   changeset = CMS.change_page(conn.assigns.page)
-   render(conn, "edit.html", page: page, changeset: changeset)
+   render(conn, "edit.html", changeset: changeset)
  end

  def create(conn, %{"page" =&gt; page_params}) do
-   case CMS.create_page(page_params) do
+   case CMS.create_page(conn.assigns.current_author, page_params) do
      {:ok, page} -&gt;
        conn
        |&gt; put_flash(:info, "Page created successfully.")
        |&gt; redirect(to: Routes.cms_page_path(conn, :show, page))
      {:error, %Ecto.Changeset{} = changeset} -&gt;
        render(conn, "new.html", changeset: changeset)
    end
  end

- def update(conn, %{"id" =&gt; id, "page" =&gt; page_params}) do
+ def update(conn, %{"page" =&gt; page_params}) do
-   page = CMS.get_page!(id)
-   case CMS.update_page(page, page_params) do
+   case CMS.update_page(conn.assigns.page, page_params) do
      {:ok, page} -&gt;
        conn
        |&gt; put_flash(:info, "Page updated successfully.")
        |&gt; redirect(to: Routes.cms_page_path(conn, :show, page))
      {:error, %Ecto.Changeset{} = changeset} -&gt;
-       render(conn, "edit.html", page: page, changeset: changeset)
+       render(conn, "edit.html", changeset: changeset)
    end
  end

- def delete(conn, %{"id" =&gt; id}) do
+ def delete(conn, _) do
-   page = CMS.get_page!(id)
-   {:ok, _page} = CMS.delete_page(page)
+   {:ok, _page} = CMS.delete_page(conn.assigns.page)

    conn
    |&gt; put_flash(:info, "Page deleted successfully.")
    |&gt; redirect(to: Routes.cms_page_path(conn, :index))
  end</pre>
<p>We modified the <code class="inline">create</code> action to grab our <code class="inline">current_author</code> from the connection assigns, which was placed there by our <code class="inline">require_existing_author</code> plug. We then passed our current author into <code class="inline">CMS.create_page</code> where it will be used to associate the author to the new page. Next, we changed the <code class="inline">update</code> action to pass the <code class="inline">conn.assigns.page</code> into <code class="inline">CMS.update_page/2</code>, rather than fetching it directly in the action. Since our <code class="inline">authorize_page</code> plug already fetched the page and placed it into the assigns, we can simply reference it here in the action. Similarly, we updated the <code class="inline">delete</code> action to pass the <code class="inline">conn.assigns.page</code> into the <code class="inline">CMS</code> rather than fetching the page in the action.</p>
<p>To complete the web changes, let's display the author when showing a page. First, open up <code class="inline">lib/hello_web/views/cms/page_view.ex</code> and add a helper function to handle formatting the author's name:</p>
<pre data-language="elixir">defmodule HelloWeb.CMS.PageView do
  use HelloWeb, :view

  alias Hello.CMS

  def author_name(%CMS.Page{author: author}) do
    author.user.name
  end
end</pre>
<p>Next, let's open up <code class="inline">lib/hello_web/templates/cms/page/show.html.eex</code> and make use of our new function:</p>
<pre data-language="elixir">+ &lt;li&gt;
+   &lt;strong&gt;Author:&lt;/strong&gt;
+   &lt;%= author_name(@page) %&gt;
+ &lt;/li&gt;
&lt;/ul&gt;</pre>
<p>Now, fire up your server with <a href="mix.tasks.phx.server"><code class="inline">mix phx.server</code></a> and try it out. Visit <a href="http://localhost:4000/cms/pages/new">http://localhost:4000/cms/pages/new</a> and save a new page.</p>
<pre data-language="elixir">Page created successfully.

Show Page Title: Home
Body: Welcome to Phoenix!
Views: 0
Author: Chris</pre>
<p>And it works! We now have two isolated contexts responsible for user accounts and content management. We coupled the content management system to accounts where necessary, while keeping each system isolated wherever possible. This gives us a great base to grow our application from.</p>
<h2 id="adding-cms-functions" class="section-heading">  Adding CMS functions </h2> <p>Just like we extended our <code class="inline">Accounts</code> context with new application-specific functions like <code class="inline">Accounts.authenticate_by_email_password/2</code>, let's extend our generated <code class="inline">CMS</code> context with new functionality. For any CMS system, the ability to track how many times a page has been viewed is essential for popularity ranks. While we could try to use the existing <code class="inline">CMS.update_page</code> function, along the lines of <code class="inline">CMS.update_page(user, page, %{views: page.views + 1})</code>, this would not only be prone to race conditions, but it would also require the caller to know too much about our CMS system. To see why the race condition exists, let's walk through the possible execution of events:</p>
<p>Intuitively, you would assume the following events:</p>
<ol>
<li>User 1 loads the page with count of 13</li>
<li>User 1 saves the page with count of 14</li>
<li>User 2 loads the page with count of 14</li>
<li>User 2 loads the page with count of 15</li>
</ol>
<p>While in practice this would happen:</p>
<ol>
<li>User 1 loads the page with count of 13</li>
<li>User 2 loads the page with count of 13</li>
<li>User 1 saves the page with count of 14</li>
<li>User 2 saves the page with count of 14</li>
</ol>
<p>The race conditions would make this an unreliable way to update the existing table since multiple callers may be updating out of date view values. There's a better way.</p>
<p>Again, let's think of a function name that describes what we want to accomplish.</p>
<pre data-language="elixir">&gt; page = CMS.inc_page_views(page)</pre>
<p>That looks great. Our callers will have no confusion over what this function does and we can wrap up the increment in an atomic operation to prevent race conditions.</p>
<p>Open up your CMS context (<code class="inline">lib/hello/cms.ex</code>), and add this new function:</p>
<pre data-language="elixir">def inc_page_views(%Page{} = page) do
  {1, [%Page{views: views}]} =
    from(p in Page, where: p.id == ^page.id, select: [:views])
    |&gt; Repo.update_all(inc: [views: 1])

  put_in(page.views, views)
end</pre>
<p>We built a query for fetching the current page given its ID which we pass to <code class="inline">Repo.update_all</code>. Ecto's <code class="inline">Repo.update_all</code> allows us to perform batch updates against the database, and is perfect for atomically updating values, such as incrementing our views count. The result of the repo operation returns the number of updated records, along with the selected schema values specified by the <code class="inline">select</code> option. When we receive the new page views, we use <code class="inline">put_in(page.views, views)</code> to place the new view count within the page.</p>
<p>With our context function in place, let's make use of it in our CMS page controller. Update your <code class="inline">show</code> action in <code class="inline">lib/hello_web/controllers/cms/page_controller.ex</code> to call our new function:</p>
<pre data-language="elixir">def show(conn, %{"id" =&gt; id}) do
  page =
    id
    |&gt; CMS.get_page!()
    |&gt; CMS.inc_page_views()

  render(conn, "show.html", page: page)
end</pre>
<p>We modified our <code class="inline">show</code> action to pipe our fetched page into <code class="inline">CMS.inc_page_views/1</code>, which will return the updated page. Then we rendered our template just as before. Let's try it out. Refresh one of your pages a few times and watch the view count increase.</p>
<p>We can also see our atomic update in action in the ecto debug logs:</p>
<pre data-language="elixir">[debug] QUERY OK source="pages" db=3.1ms
UPDATE "pages" AS p0 SET "views" = p0."views" + $1 WHERE (p0."id" = $2)
RETURNING p0."views" [1, 3]</pre>
<p>Good work!</p>
<p>As we've seen, designing with contexts gives you a solid foundation to grow your application from. Using discrete, well-defined APIs that expose the intent of your system allows you to write more maintainable applications with reusable code.</p>
<h2 id="faq" class="section-heading">  FAQ </h2> <h3 id="returning-ecto-structures-from-context-apis" class="section-heading">  Returning Ecto structures from context APIs </h3> <p>As we explored the context API, you might have wondered:</p>
<blockquote><p>If one of the goals of our context is to encapsulate Ecto Repo access, why does <code class="inline">create_user/1</code> return an <code class="inline">Ecto.Changeset</code> struct when we fail to create a user?</p></blockquote>
<p>The answer is we've decided to expose <code class="inline">%Ecto.Changeset{}</code> as a public <em>data-structure</em> in our application. We saw before how changesets allow us to track field changes, perform validations, and generate error messages. Its use here is decoupled from the private Repo access and Ecto changeset API internals. We're exposing a data structure that the caller understands which contains the rich information like field errors. Conveniently for us, the <code class="inline">phoenix_ecto</code> project implements the necessary <a href="phoenix.param"><code class="inline">Phoenix.Param</code></a> and <a href="../../phoenix_html/phoenix.html.formdata"><code class="inline">Phoenix.HTML.FormData</code></a> protocols which know how to handle <code class="inline">%Ecto.Changeset{}</code>'s for things like form generation and error messages. You can also think about it as being as if you had defined your own <code class="inline">%Accounts.Changes{}</code> struct for the same purpose and implemented the Phoenix protocols for the web-layer integration.</p>
<h3 id="strategies-for-cross-context-workflows" class="section-heading">  Strategies for cross-context workflows </h3> <p>Our CMS context supports lazily creating authors in the system when a user decides to publish page content. This makes sense for our use case because not all users of our system will be CMS authors. But what if our use case were for when all users of our app are indeed authors?</p>
<p>If we require a <code class="inline">CMS.Author</code> to exist every time an <code class="inline">Accounts.User</code> is created, we have to think carefully where to place this dependency. We know our <code class="inline">CMS</code> context depends on the <code class="inline">Accounts</code> context, but it's important to avoid cyclic dependencies across our contexts. For example, imagine we changed our <code class="inline">Accounts.create_user</code> function to:</p>
<pre data-language="elixir">def create_user(attrs) do
  %User{}
  |&gt; User.changeset(attrs)
  |&gt; Ecto.Changeset.cast_assoc(:credential, with: &amp;Credential.changeset/2)
  |&gt; Ecto.Changeset.put_assoc(:author, %Author{...})
  |&gt; Repo.insert()
end</pre>
<p>This may accomplish what we want, but now we need to wire up the schema relationships in the <code class="inline">Accounts</code> context to the <code class="inline">CMS</code> author. Worse, we have now taken our isolated <code class="inline">Accounts</code> context and required it to know about a content management system, which in turn knows about <code class="inline">Accounts</code> too. With time, cyclic dependencies would blurry the lines between contexts, which is not what we want for isolated responsibilities in our application. There's a better way to handle these requirements.</p>
<p>If you find yourself in similar situations where you feel your use case is requiring you to create circular dependencies across contexts, it's a sign you need a new context in the system to handle these application requirements. In our case, what we really want is an interface that handles all requirements when a user is created or registers in our application. To handle this, we could create a <code class="inline">UserRegistration</code> context, which calls into both the <code class="inline">Accounts</code> and <code class="inline">CMS</code> APIs to create a user, then associate a CMS author. Not only would this allow our Accounts to remain as isolated as possible, it gives us a clear, obvious API to handle <code class="inline">UserRegistration</code> needs in the system. If you take this approach, you can also use tools like <code class="inline">Ecto.Multi</code> to handle transactions across different context operations without deeply coupling the internal database calls. Part of our <code class="inline">UserRegistration</code> API could look something like this:</p>
<pre data-language="elixir">defmodule Hello.UserRegistration do
  alias Ecto.Multi
  alias Hello.{Accounts, CMS, Repo}

  def register_user(params) do
    Multi.new()
    |&gt; Multi.run(:user, fn _repo, _changes_so_far -&gt; Accounts.create_user(params) end)
    |&gt; Multi.run(:author, fn _repo, %{user: user} -&gt;
      {:ok, CMS.ensure_author_exists(user)}
    end)
    |&gt; Repo.transaction()
  end
end</pre>
<p>We can take advantage of <code class="inline">Ecto.Multi</code> to create a pipeline of operations that can be run inside a transaction of our <code class="inline">Repo</code>. If any given operation fails, the transaction will be rolled back and an error will be returned containing which operation failed, as well as the changes up to that point. In our <code class="inline">register_user/1</code> example, we specified two operations, one that calls into <code class="inline">Accounts.create_user/1</code> and another that passes the newly created user to <code class="inline">CMS.ensure_author_exists/1</code>. The final step of our function is to invoke the operations with <code class="inline">Repo.transaction/1</code>.</p>
<p>The <code class="inline">UserRegistration</code> setup is likely simpler to implement than the dynamic author system we built â€“ we decided to take the harder path exactly because those are decisions developers take on their applications every day.</p><div class="_attribution">
  <p class="_attribution-p">
    <a href="https://hexdocs.pm/phoenix/1.5.7/contexts.html" class="_attribution-link" target="_blank">https://hexdocs.pm/phoenix/1.5.7/contexts.html</a>
  </p>
</div>
