 <h1 id="if-varnil">if var.nil?</h1> <p>If an <code>if</code>'s condition is <code>var.nil?</code> then the type of <code>var</code> in the <code>then</code> branch is known by the compiler to be <code>Nil</code>, and to be known as non-<code>Nil</code> in the <code>else</code> branch:</p> <div class="crystal highlight"><pre data-language="crystal">a = some_condition ? nil : 3
if a.nil?
  # here a is Nil
else
  # here a is Int32
end
</pre></div> <h2 id="instance-variables">Instance Variables</h2> <p>Type restriction through <code>if var.nil?</code> only occurs with local variables. The type of an instance variable in a similar code example to the one above will still be nilable and will throw a compile error since <code>greet</code> expects a <code>String</code> in the <code>unless</code> branch.</p> <div class="crystal highlight"><pre data-language="crystal">class Person
  property name : String?

  def greet
    unless @name.nil?
      puts "Hello, #{@name.upcase}" # Error: undefined method 'upcase' for Nil (compile-time type is (String | Nil))
    else
      puts "Hello"
    end
  end
end

Person.new.greet
</pre></div> <p>You can solve this by storing the value in a local variable first:</p> <div class="crystal highlight"><pre data-language="crystal">def greet
  name = @name
  unless name.nil?
    puts "Hello, #{name.upcase}" # name will be String - no compile error
  else
    puts "Hello"
  end
end
</pre></div> <p>This is a byproduct of multi-threading in Crystal. Due to the existence of Fibers, Crystal does not know at compile-time whether the instance variable will still be non-<code>Nil</code> when the usage in the <code>if</code> branch is reached.</p><div class="_attribution">
  <p class="_attribution-p">
    <a href="https://crystal-lang.org/reference/1.5/syntax_and_semantics/if_var_nil.html" class="_attribution-link" target="_blank">https://crystal-lang.org/reference/1.5/syntax_and_semantics/if_var_nil.html</a>
  </p>
</div>
