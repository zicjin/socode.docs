 <h1 id="overloading">Overloading</h1> <p>We can define a <code>become_older</code> method that accepts a number indicating the years to grow:</p> <div class="crystal highlight"><pre data-language="crystal">class Person
  getter :age

  def initialize(@name : String, @age : Int = 0)
  end

  def become_older
    @age += 1
  end

  def become_older(years)
    @age += years
  end
end

john = Person.new "John"
john.age # =&gt; 0

john.become_older
john.age # =&gt; 1

john.become_older 5
john.age # =&gt; 6
</pre></div> <p>That is, you can have different methods with the same name and different number of parameters and they will be considered as separate methods. This is called <em>method overloading</em>.</p> <p>Methods overload by several criteria:</p> <ul> <li>The number of parameters</li> <li>The type restrictions applied to parameters</li> <li>The names of required named parameters</li> <li>Whether the method accepts a <a href="blocks_and_procs">block</a> or not</li> </ul> <p>For example, we can define four different <code>become_older</code> methods:</p> <div class="crystal highlight"><pre data-language="crystal">class Person
  @age = 0

  # Increases age by one
  def become_older
    @age += 1
  end

  # Increases age by the given number of years
  def become_older(years : Int32)
    @age += years
  end

  # Increases age by the given number of years, as a String
  def become_older(years : String)
    @age += years.to_i
  end

  # Yields the current age of this person and increases
  # its age by the value returned by the block
  def become_older
    @age += yield @age
  end
end

person = Person.new "John"

person.become_older
person.age # =&gt; 1

person.become_older 5
person.age # =&gt; 6

person.become_older "12"
person.age # =&gt; 18

person.become_older do |current_age|
  current_age &lt; 20 ? 10 : 30
end
person.age # =&gt; 28
</pre></div> <p>Note that in the case of the method that yields, the compiler figured this out because there's a <code>yield</code> expression. To make this more explicit, you can add a dummy <code>&amp;block</code> parameter at the end:</p> <div class="crystal highlight"><pre data-language="crystal">class Person
  @age = 0

  def become_older(&amp;block)
    @age += yield @age
  end
end
</pre></div> <p>In generated documentation the dummy <code>&amp;block</code> method will always appear, regardless of you writing it or not.</p> <p>Given the same number of parameters, the compiler will try to sort them by leaving the less restrictive ones to the end:</p> <div class="crystal highlight"><pre data-language="crystal">class Person
  @age = 0

  # First, this method is defined
  def become_older(age)
    @age += age
  end

  # Since "String" is more restrictive than no restriction
  # at all, the compiler puts this method before the previous
  # one when considering which overload matches.
  def become_older(age : String)
    @age += age.to_i
  end
end

person = Person.new "John"

# Invokes the first definition
person.become_older 20

# Invokes the second definition
person.become_older "12"
</pre></div> <p>However, the compiler cannot always figure out the order because there isn't always a total ordering, so it's always better to put less restrictive methods at the end.</p><div class="_attribution">
  <p class="_attribution-p">
    <a href="https://crystal-lang.org/reference/1.5/syntax_and_semantics/overloading.html" class="_attribution-link" target="_blank">https://crystal-lang.org/reference/1.5/syntax_and_semantics/overloading.html</a>
  </p>
</div>
