 <h1 id="case">case</h1> <p>A <code>case</code> is a control expression which functions a bit like pattern matching. It allows writing a chain of if-else-if with a small change in semantic and some more powerful constructs.</p> <p>In its basic form, it allows matching a value against other values:</p> <div class="crystal highlight"><pre data-language="crystal">case exp
when value1, value2
  do_something
when value3
  do_something_else
else
  do_another_thing
end

# The above is the same as:
tmp = exp
if value1 === tmp || value2 === tmp
  do_something
elsif value3 === tmp
  do_something_else
else
  do_another_thing
end
</pre></div> <p>For comparing an expression against a <code>case</code>'s value the <em>case equality operator</em> <code>===</code> is used. It is defined as a method on <a href="https://crystal-lang.org/api/1.5.0/Object.html#%3D%3D%3D%28other%29-instance-method"><code>Object</code></a> and can be overridden by subclasses to provide meaningful semantics in case statements. For example, <a href="https://crystal-lang.org/api/1.5.0/Class.html#%3D%3D%3D%28other%29-instance-method"><code>Class</code></a> defines case equality as when an object is an instance of that class, <a href="https://crystal-lang.org/api/1.5.0/Regex.html#%3D%3D%3D%28other%3AString%29-instance-method"><code>Regex</code></a> as when the value matches the regular expression and <a href="https://crystal-lang.org/api/1.5.0/Range.html#%3D%3D%3D%28value%29-instance-method"><code>Range</code></a> as when the value is included in that range.</p> <p>If a <code>when</code>'s expression is a type, <code>is_a?</code> is used. Additionally, if the case expression is a variable or a variable assignment the type of the variable is restricted:</p> <div class="crystal highlight"><pre data-language="crystal">case var
when String
  # var : String
  do_something
when Int32
  # var : Int32
  do_something_else
else
  # here var is neither a String nor an Int32
  do_another_thing
end

# The above is the same as:
if var.is_a?(String)
  do_something
elsif var.is_a?(Int32)
  do_something_else
else
  do_another_thing
end
</pre></div> <p>You can invoke a method on the <code>case</code>'s expression in a <code>when</code> by using the implicit-object syntax:</p> <div class="crystal highlight"><pre data-language="crystal">case num
when .even?
  do_something
when .odd?
  do_something_else
end

# The above is the same as:
tmp = num
if tmp.even?
  do_something
elsif tmp.odd?
  do_something_else
end
</pre></div> <p>You may use <code>then</code> after the <code>when</code> condition to place the body on a single line.</p> <div class="crystal highlight"><pre data-language="crystal">case exp
when value1, value2 then do_something
when value3         then do_something_else
else                     do_another_thing
end
</pre></div> <p>Finally, you can omit the <code>case</code>'s value:</p> <div class="crystal highlight"><pre data-language="crystal">case
when cond1, cond2
  do_something
when cond3
  do_something_else
end

# The above is the same as:
if cond1 || cond2
  do_something
elsif cond3
  do_something_else
end
</pre></div> <p>This sometimes leads to code that is more natural to read.</p> <h2 id="tuple-literal">Tuple literal</h2> <p>When a case expression is a tuple literal there are a few semantic differences if a <code>when</code> condition is also a tuple literal.</p> <h3 id="tuple-size-must-match">Tuple size must match</h3> <div class="highlight"><pre data-language="crystal">case {value1, value2}
when {0, 0} # OK, 2 elements
  # ...
when {1, 2, 3} # Syntax error: wrong number of tuple elements (given 3, expected 2)
  # ...
end
</pre></div> <h3 id="underscore-allowed">Underscore allowed</h3> <div class="crystal highlight"><pre data-language="crystal">case {value1, value2}
when {0, _}
  # Matches if 0 === value1, no test done against value2
when {_, 0}
  # Matches if 0 === value2, no test done against value1
end
</pre></div> <h3 id="implicit-object-allowed">Implicit-object allowed</h3> <div class="crystal highlight"><pre data-language="crystal">case {value1, value2}
when {.even?, .odd?}
  # Matches if value1.even? &amp;&amp; value2.odd?
end
</pre></div> <h3 id="comparing-against-a-type-will-perform-an-is_a-check">Comparing against a type will perform an is_a? check</h3> <div class="crystal highlight"><pre data-language="crystal">case {value1, value2}
when {String, Int32}
  # Matches if value1.is_a?(String) &amp;&amp; value2.is_a?(Int32)
  # The type of value1 is known to be a String by the compiler,
  # and the type of value2 is known to be an Int32
end
</pre></div> <h2 id="exhaustive-case">Exhaustive case</h2> <p>Using <code>in</code> instead of <code>when</code> produces an exhaustive case expression; in an exhaustive case, it is a compile-time error to omit any of the required <code>in</code> conditions. An exhaustive <code>case</code> cannot contain any <code>when</code> or <code>else</code> clauses.</p> <p>The compiler supports the following <code>in</code> conditions:</p> <h3 id="union-type-checks">Union type checks</h3> <p>If <code>case</code>'s expression is a union value, each of the union types may be used as a condition:</p> <div class="crystal highlight"><pre data-language="crystal"># var : (Bool | Char | String)?
case var
in String
  # var : String
in Char
  # var : Char
in Bool
  # var : Bool
in nil # or Nil, but .nil? is not allowed
  # var : Nil
end
</pre></div> <h3 id="bool-values">Bool values</h3> <p>If <code>case</code>'s expression is a <code>Bool</code> value, the <code>true</code> and <code>false</code> literals may be used as conditions:</p> <div class="crystal highlight"><pre data-language="crystal"># var : Bool
case var
in true
  do_something
in false
  do_something_else
end
</pre></div> <h3 id="enum-values">Enum values</h3> <p>If <code>case</code>'s expression is a non-flags enum value, its members may be used as conditions, either as constant or predicate method.</p> <div class="crystal highlight"><pre data-language="crystal">enum Foo
  X
  Y
  Z
end

# var : Foo
case var
in Foo::X
  # var == Foo::X
in .y?
  # var == Foo::Y
in .z? # :z is not allowed
  # var == Foo::Z
end
</pre></div> <h3 id="tuple-literals">Tuple literals</h3> <p>The conditions must exhaust all possible combinations of the <code>case</code> expression's elements:</p> <div class="crystal highlight"><pre data-language="crystal"># value1, value2 : Bool
case {value1, value2}
in {true, _}
  # value1 is true, value2 can be true or false
  do_something
in {_, false}
  # here value1 is false, and value2 is also false
  do_something_else
end

# Error: case is not exhaustive.
#
# Missing cases:
#  - {false, true}
</pre></div><div class="_attribution">
  <p class="_attribution-p">
    <a href="https://crystal-lang.org/reference/1.5/syntax_and_semantics/case.html" class="_attribution-link" target="_blank">https://crystal-lang.org/reference/1.5/syntax_and_semantics/case.html</a>
  </p>
</div>
