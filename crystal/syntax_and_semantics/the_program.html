 <h1 id="the-program">The Program</h1> <p>The program is a global object in which you can define types, methods and file-local variables.</p> <div class="crystal highlight"><pre data-language="crystal"># Defines a method in the program
def add(x, y)
  x + y
end

# Invokes the add method in the program
add(1, 2) # =&gt; 3
</pre></div> <p>A method's value is the value of its last expression; there's no need for explicit <code>return</code> expressions. However, explicit <code>return</code> expressions are possible:</p> <div class="crystal highlight"><pre data-language="crystal">def even?(num)
  if num % 2 == 0
    return true
  end

  return false
end
</pre></div> <p>When invoking a method without a receiver, like <code>add(1, 2)</code>, it will be searched for in the program if not found in the current type or any of its ancestors.</p> <div class="crystal highlight"><pre data-language="crystal">def add(x, y)
  x + y
end

class Foo
  def bar
    # invokes the program's add method
    add(1, 2)

    # invokes Foo's baz method
    baz(1, 2)
  end

  def baz(x, y)
    x * y
  end
end
</pre></div> <p>If you want to invoke the program's method, even though the current type defines a method with the same name, prefix the call with <code>::</code>:</p> <div class="crystal highlight"><pre data-language="crystal">def baz(x, y)
  x + y
end

class Foo
  def bar
    baz(4, 2)   # =&gt; 2
    ::baz(4, 2) # =&gt; 6
  end

  def baz(x, y)
    x - y
  end
end
</pre></div> <p>Variables declared in a program are not visible inside methods:</p> <div class="crystal highlight"><pre data-language="crystal">x = 1

def add(y)
  x + y # error: undefined local variable or method 'x'
end

add(2)
</pre></div> <p>Parentheses in method invocations are optional:</p> <div class="crystal highlight"><pre data-language="crystal">add 1, 2 # same as add(1, 2)
</pre></div> <h2 id="main-code">Main code</h2> <p>Main code, the code that is run when you compile and run a program, can be written directly in a source file without the need to put it in a special "main" method:</p> <div class="crystal highlight"><pre data-language="crystal"># This is a program that prints "Hello Crystal!"
puts "Hello Crystal!"
</pre></div> <p>Main code can also be inside type declarations:</p> <div class="crystal highlight"><pre data-language="crystal"># This is a program that prints "Hello"
class Hello
  # 'self' here is the Hello class
  puts self
end
</pre></div><div class="_attribution">
  <p class="_attribution-p">
    <a href="https://crystal-lang.org/reference/1.3/syntax_and_semantics/the_program.html" class="_attribution-link" target="_blank">https://crystal-lang.org/reference/1.3/syntax_and_semantics/the_program.html</a>
  </p>
</div>
