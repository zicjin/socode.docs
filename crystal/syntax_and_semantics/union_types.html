 <h1 id="union-types">Union types</h1> <p>The type of a variable or expression can consist of multiple types. This is called a union type. For example, when assigning to a same variable inside different <a href="if">if</a> branches:</p> <div class="crystal highlight"><pre data-language="crystal">if 1 + 2 == 3
  a = 1
else
  a = "hello"
end

a # : Int32 | String
</pre></div> <p>At the end of the if, <code>a</code> will have the <code>Int32 | String</code> type, read "the union of Int32 and String". This union type is created automatically by the compiler. At runtime, <code>a</code> will of course be of one type only. This can be seen by invoking the <code>class</code> method:</p> <div class="crystal highlight"><pre data-language="crystal"># The runtime type
a.class # =&gt; Int32
</pre></div> <p>The compile-time type can be seen by using <a href="typeof">typeof</a>:</p> <div class="crystal highlight"><pre data-language="crystal"># The compile-time type
typeof(a) # =&gt; Int32 | String
</pre></div> <p>A union can consist of an arbitrary large number of types. When invoking a method on an expression whose type is a union type, all types in the union must respond to the method, otherwise a compile-time error is given. The type of the method call is the union type of the return types of those methods.</p> <div class="crystal highlight"><pre data-language="crystal"># to_s is defined for Int32 and String, it returns String
a.to_s # =&gt; String

a + 1 # Error, because String#+(Int32) isn't defined
</pre></div> <p>If necessary a variable can be defined as a union type at compile time</p> <div class="crystal highlight"><pre data-language="crystal"># set the compile-time type
a = 0.as(Int32 | Nil | String)
typeof(a) # =&gt; Int32 | Nil | String
</pre></div> <h2 id="union-types-rules">Union types rules</h2> <p>In the general case, when two types <code>T1</code> and <code>T2</code> are combined, the result is a union <code>T1 | T2</code>. However, there are a few cases where the resulting type is a different type.</p> <h3 id="union-of-classes-and-structs-under-the-same-hierarchy">Union of classes and structs under the same hierarchy</h3> <p>If <code>T1</code> and <code>T2</code> are under the same hierarchy, and their nearest common ancestor <code>Parent</code> is not <code>Reference</code>, <code>Struct</code>, <code>Int</code>, <code>Float</code> nor <code>Value</code>, the resulting type is <code>Parent+</code>. This is called a virtual type, which basically means the compiler will now see the type as being <code>Parent</code> or any of its subtypes.</p> <p>For example:</p> <div class="crystal highlight"><pre data-language="crystal">class Foo
end

class Bar &lt; Foo
end

class Baz &lt; Foo
end

bar = Bar.new
baz = Baz.new

# Here foo's type will be Bar | Baz,
# but because both Bar and Baz inherit from Foo,
# the resulting type is Foo+
foo = rand &lt; 0.5 ? bar : baz
typeof(foo) # =&gt; Foo+
</pre></div> <h3 id="union-of-tuples-of-the-same-size">Union of tuples of the same size</h3> <p>The union of two tuples of the same size results in a tuple type that has the union of the types in each position.</p> <p>For example:</p> <div class="crystal highlight"><pre data-language="crystal">t1 = {1, "hi"}   # Tuple(Int32, String)
t2 = {true, nil} # Tuple(Bool, Nil)

t3 = rand &lt; 0.5 ? t1 : t2
typeof(t3) # Tuple(Int32 | Bool, String | Nil)
</pre></div> <h3 id="union-of-named-tuples-with-the-same-keys">Union of named tuples with the same keys</h3> <p>The union of two named tuples with the same keys (regardless of their order) results in a named tuple type that has the union of the types in each key. The order of the keys will be the ones from the tuple on the left hand side.</p> <p>For example:</p> <div class="crystal highlight"><pre data-language="crystal">t1 = {x: 1, y: "hi"}   # Tuple(x: Int32, y: String)
t2 = {y: true, x: nil} # Tuple(y: Bool, x: Nil)

t3 = rand &lt; 0.5 ? t1 : t2
typeof(t3) # NamedTuple(x: Int32 | Nil, y: String | Bool)
</pre></div><div class="_attribution">
  <p class="_attribution-p">
    <a href="https://crystal-lang.org/reference/1.3/syntax_and_semantics/union_types.html" class="_attribution-link" target="_blank">https://crystal-lang.org/reference/1.3/syntax_and_semantics/union_types.html</a>
  </p>
</div>
