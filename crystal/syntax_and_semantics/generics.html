 <h1 id="generics">Generics</h1> <p>Generics allow you to parameterize a type based on other type. Consider a Box type:</p> <div class="crystal highlight"><pre data-language="crystal">class MyBox(T)
  def initialize(@value : T)
  end

  def value
    @value
  end
end

int_box = MyBox(Int32).new(1)
int_box.value # =&gt; 1 (Int32)

string_box = MyBox(String).new("hello")
string_box.value # =&gt; "hello" (String)

another_box = MyBox(String).new(1) # Error, Int32 doesn't match String
</pre></div> <p>Generics are especially useful for implementing collection types. <code>Array</code>, <code>Hash</code>, <code>Set</code> are generic types, as is <code>Pointer</code>.</p> <p>More than one type parameter is allowed:</p> <div class="crystal highlight"><pre data-language="crystal">class MyDictionary(K, V)
end
</pre></div> <p>Any name can be used for type parameters:</p> <div class="crystal highlight"><pre data-language="crystal">class MyDictionary(KeyType, ValueType)
end
</pre></div> <h2 id="type-variables-inference">Type variables inference</h2> <p>Type restrictions in a generic type's constructor are free variables when type arguments were not specified, and then are used to infer them. For example:</p> <div class="crystal highlight"><pre data-language="crystal">MyBox.new(1)       # : MyBox(Int32)
MyBox.new("hello") # : MyBox(String)
</pre></div> <p>In the above code we didn't have to specify the type arguments of <code>MyBox</code>, the compiler inferred them following this process:</p> <ul> <li>
<code>MyBox.new(value)</code> delegates to <code>initialize(@value : T)</code>
</li> <li>
<code>T</code> isn't bound to a type yet, so the compiler binds it to the type of the given argument</li> </ul> <p>In this way generic types are less tedious to work with.</p> <h2 id="generic-structs-and-modules">Generic structs and modules</h2> <p>Structs and modules can be generic too. When a module is generic you include it like this:</p> <div class="crystal highlight"><pre data-language="crystal">module Moo(T)
  def t
    T
  end
end

class Foo(U)
  include Moo(U)

  def initialize(@value : U)
  end
end

foo = Foo.new(1)
foo.t # Int32
</pre></div> <p>Note that in the above example <code>T</code> becomes <code>Int32</code> because <code>Foo.new(1)</code> makes <code>U</code> become <code>Int32</code>, which in turn makes <code>T</code> become <code>Int32</code> via the inclusion of the generic module.</p> <h2 id="generic-types-inheritance">Generic types inheritance</h2> <p>Generic classes and structs can be inherited. When inheriting you can specify an instance of the generic type, or delegate type variables:</p> <div class="crystal highlight"><pre data-language="crystal">class Parent(T)
end

class Int32Child &lt; Parent(Int32)
end

class GenericChild(T) &lt; Parent(T)
end
</pre></div> <h2 id="generics-with-variable-number-of-arguments">Generics with variable number of arguments</h2> <p>We may define a Generic class with a variable number of arguments using the <a href="operators#splats">splat operator</a>.</p> <p>Let's see an example where we define a Generic class called <code>Foo</code> and then we will use it with different number of type variables:</p> <div class="crystal-play highlight"><pre data-language="crystal">class Foo(*T)
  getter content

  def initialize(*@content : *T)
  end
end

# 2 type variables:
# (explicitly specifying type variables)
foo = Foo(Int32, String).new(42, "Life, the Universe, and Everything")

p typeof(foo) # =&gt; Foo(Int32, String)
p foo.content # =&gt; {42, "Life, the Universe, and Everything"}

# 3 type variables:
# (type variables inferred by the compiler)
bar = Foo.new("Hello", ["Crystal", "!"], 140)
p typeof(bar) # =&gt; Foo(String, Array(String), Int32)
</pre></div> <p>In the following example we define classes by inheritance, specifying instances for the generic types:</p> <div class="crystal highlight"><pre data-language="crystal">class Parent(*T)
end

# We define `StringChild` inheriting from `Parent` class
# using `String` for generic type argument:
class StringChild &lt; Parent(String)
end

# We define `Int32StringChild` inheriting from `Parent` class
# using `Int32` and `String` for generic type arguments:
class Int32StringChild &lt; Parent(Int32, String)
end
</pre></div> <p>And if we need to instantiate a <code>class</code> with 0 arguments? In that case we may do:</p> <div class="crystal-play highlight"><pre data-language="crystal">class Parent(*T)
end

foo = Parent().new
p typeof(foo) # =&gt; Parent()
</pre></div> <p>But we should not mistake 0 arguments with not specifying the generic type variables. The following examples will raise an error:</p> <div class="crystal highlight"><pre data-language="crystal">class Parent(*T)
end

foo = Parent.new # Error: can't infer the type parameter T for the generic class Parent(*T). Please provide it explicitly

class Foo &lt; Parent # Error: generic type arguments must be specified when inheriting Parent(*T)
end
</pre></div><div class="_attribution">
  <p class="_attribution-p">
    <a href="https://crystal-lang.org/reference/1.5/syntax_and_semantics/generics.html" class="_attribution-link" target="_blank">https://crystal-lang.org/reference/1.5/syntax_and_semantics/generics.html</a>
  </p>
</div>
