 <h1 id="type-restrictions">Type restrictions</h1> <p>Type restrictions are applied to method parameters to restrict the types accepted by that method.</p> <div class="crystal highlight"><pre data-language="crystal">def add(x : Number, y : Number)
  x + y
end

# Ok
add 1, 2

# Error: no overload matches 'add' with types Bool, Bool
add true, false
</pre></div> <p>Note that if we had defined <code>add</code> without type restrictions, we would also have gotten a compile time error:</p> <div class="crystal highlight"><pre data-language="crystal">def add(x, y)
  x + y
end

add true, false
</pre></div> <p>The above code gives this compile error:</p> <div class="highlight"><pre data-language="crystal">Error in foo.cr:6: instantiating 'add(Bool, Bool)'

add true, false
^~~

in foo.cr:2: undefined method '+' for Bool

  x + y
    ^
</pre></div> <p>This is because when you invoke <code>add</code>, it is instantiated with the types of the arguments: every method invocation with a different type combination results in a different method instantiation.</p> <p>The only difference is that the first error message is a little more clear, but both definitions are safe in that you will get a compile time error anyway. So, in general, it's preferable not to specify type restrictions and almost only use them to define different method overloads. This results in more generic, reusable code. For example, if we define a class that has a <code>+</code> method but isn't a <code>Number</code>, we can use the <code>add</code> method that doesn't have type restrictions, but we can't use the <code>add</code> method that has restrictions.</p> <div class="crystal highlight"><pre data-language="crystal"># A class that has a + method but isn't a Number
class Six
  def +(other)
    6 + other
  end
end

# add method without type restrictions
def add(x, y)
  x + y
end

# OK
add Six.new, 10

# add method with type restrictions
def restricted_add(x : Number, y : Number)
  x + y
end

# Error: no overload matches 'restricted_add' with types Six, Int32
restricted_add Six.new, 10
</pre></div> <p>Refer to the <a href="type_grammar">type grammar</a> for the notation used in type restrictions.</p> <p>Note that type restrictions do not apply to the variables inside the actual methods.</p> <div class="crystal highlight"><pre data-language="crystal">def handle_path(path : String)
  path = Path.new(path) # *path* is now of the type Path
  # Do something with *path*
end
</pre></div> <h2 id="self-restriction">self restriction</h2> <p>A special type restriction is <code>self</code>:</p> <div class="crystal highlight"><pre data-language="crystal">class Person
  def ==(other : self)
    other.name == name
  end

  def ==(other)
    false
  end
end

john = Person.new "John"
another_john = Person.new "John"
peter = Person.new "Peter"

john == another_john # =&gt; true
john == peter        # =&gt; false (names differ)
john == 1            # =&gt; false (because 1 is not a Person)
</pre></div> <p>In the previous example <code>self</code> is the same as writing <code>Person</code>. But, in general, <code>self</code> is the same as writing the type that will finally own that method, which, when modules are involved, becomes more useful.</p> <p>As a side note, since <code>Person</code> inherits <code>Reference</code> the second definition of <code>==</code> is not needed, since it's already defined in <code>Reference</code>.</p> <p>Note that <code>self</code> always represents a match against an instance type, even in class methods:</p> <div class="crystal highlight"><pre data-language="crystal">class Person
  getter name : String

  def initialize(@name)
  end

  def self.compare(p1 : self, p2 : self)
    p1.name == p2.name
  end
end

john = Person.new "John"
peter = Person.new "Peter"

Person.compare(john, peter) # OK
</pre></div> <p>You can use <code>self.class</code> to restrict to the Person type. The next section talks about the <code>.class</code> suffix in type restrictions.</p> <h2 id="classes-as-restrictions">Classes as restrictions</h2> <p>Using, for example, <code>Int32</code> as a type restriction makes the method only accept instances of <code>Int32</code>:</p> <div class="crystal highlight"><pre data-language="crystal">def foo(x : Int32)
end

foo 1       # OK
foo "hello" # Error
</pre></div> <p>If you want a method to only accept the type Int32 (not instances of it), you use <code>.class</code>:</p> <div class="crystal highlight"><pre data-language="crystal">def foo(x : Int32.class)
end

foo Int32  # OK
foo String # Error
</pre></div> <p>The above is useful for providing overloads based on types, not instances:</p> <div class="crystal highlight"><pre data-language="crystal">def foo(x : Int32.class)
  puts "Got Int32"
end

def foo(x : String.class)
  puts "Got String"
end

foo Int32  # prints "Got Int32"
foo String # prints "Got String"
</pre></div> <h2 id="type-restrictions-in-splats">Type restrictions in splats</h2> <p>You can specify type restrictions in splats:</p> <div class="crystal highlight"><pre data-language="crystal">def foo(*args : Int32)
end

def foo(*args : String)
end

foo 1, 2, 3       # OK, invokes first overload
foo "a", "b", "c" # OK, invokes second overload
foo 1, 2, "hello" # Error
foo()             # Error
</pre></div> <p>When specifying a type, all elements in a tuple must match that type. Additionally, the empty-tuple doesn't match any of the above cases. If you want to support the empty-tuple case, add another overload:</p> <div class="crystal highlight"><pre data-language="crystal">def foo
  # This is the empty-tuple case
end
</pre></div> <p>A simple way to match against one or more elements of any type is to use <code>Object</code> as a restriction:</p> <div class="crystal highlight"><pre data-language="crystal">def foo(*args : Object)
end

foo()       # Error
foo(1)      # OK
foo(1, "x") # OK
</pre></div> <h2 id="free-variables">Free variables</h2> <p>You can make a type restriction take the type of an argument, or part of the type of an argument, using <code>forall</code>:</p> <div class="crystal highlight"><pre data-language="crystal">def foo(x : T) forall T
  T
end

foo(1)       # =&gt; Int32
foo("hello") # =&gt; String
</pre></div> <p>That is, <code>T</code> becomes the type that was effectively used to instantiate the method.</p> <p>A free variable can be used to extract the type argument of a generic type within a type restriction:</p> <div class="crystal highlight"><pre data-language="crystal">def foo(x : Array(T)) forall T
  T
end

foo([1, 2])   # =&gt; Int32
foo([1, "a"]) # =&gt; (Int32 | String)
</pre></div> <p>To create a method that accepts a type name, rather than an instance of a type, append <code>.class</code> to a free variable in the type restriction:</p> <div class="crystal highlight"><pre data-language="crystal">def foo(x : T.class) forall T
  Array(T)
end

foo(Int32)  # =&gt; Array(Int32)
foo(String) # =&gt; Array(String)
</pre></div> <p>Multiple free variables can be specified too, for matching types of multiple arguments:</p> <div class="crystal highlight"><pre data-language="crystal">def push(element : T, array : Array(T)) forall T
  array &lt;&lt; element
end

push(4, [1, 2, 3])      # OK
push("oops", [1, 2, 3]) # Error
</pre></div><div class="_attribution">
  <p class="_attribution-p">
    <a href="https://crystal-lang.org/reference/1.3/syntax_and_semantics/type_restrictions.html" class="_attribution-link" target="_blank">https://crystal-lang.org/reference/1.3/syntax_and_semantics/type_restrictions.html</a>
  </p>
</div>
