 <h1 id="if">if</h1> <p>An <code>if</code> evaluates the given branch if its condition is <em>truthy</em>. Otherwise, it evaluates the <code>else</code> branch if present.</p> <div class="crystal highlight"><pre data-language="crystal">a = 1
if a &gt; 0
  a = 10
end
a # =&gt; 10

b = 1
if b &gt; 2
  b = 10
else
  b = 20
end
b # =&gt; 20
</pre></div> <p>To write a chain of if-else-if you use <code>elsif</code>:</p> <div class="crystal highlight"><pre data-language="crystal">if some_condition
  do_something
elsif some_other_condition
  do_something_else
else
  do_that
end
</pre></div> <p>After an <code>if</code>, a variable’s type depends on the type of the expressions used in both branches.</p> <div class="crystal highlight"><pre data-language="crystal">a = 1
if some_condition
  a = "hello"
else
  a = true
end
# a : String | Bool

b = 1
if some_condition
  b = "hello"
end
# b : Int32 | String

if some_condition
  c = 1
else
  c = "hello"
end
# c : Int32 | String

if some_condition
  d = 1
end
# d : Int32 | Nil
</pre></div> <p>Note that if a variable is declared inside one of the branches but not in the other one, at the end of the <code>if</code> it will also contain the <code>Nil</code> type.</p> <p>Inside an <code>if</code>'s branch the type of a variable is the one it got assigned in that branch, or the one that it had before the branch if it was not reassigned:</p> <div class="crystal highlight"><pre data-language="crystal">a = 1
if some_condition
  a = "hello"
  # a : String
  a.size
end
# a : String | Int32
</pre></div> <p>That is, a variable’s type is the type of the last expression(s) assigned to it.</p> <p>If one of the branches never reaches past the end of an <code>if</code>, like in the case of a <code>return</code>, <code>next</code>, <code>break</code> or <code>raise</code>, that type is not considered at the end of the <code>if</code>:</p> <div class="crystal highlight"><pre data-language="crystal">if some_condition
  e = 1
else
  e = "hello"
  # e : String
  return
end
# e : Int32
</pre></div><div class="_attribution">
  <p class="_attribution-p">
    <a href="https://crystal-lang.org/reference/1.3/syntax_and_semantics/if.html" class="_attribution-link" target="_blank">https://crystal-lang.org/reference/1.3/syntax_and_semantics/if.html</a>
  </p>
</div>
