 <h1 id="proc">Proc</h1> <p>A <a href="https://crystal-lang.org/api/1.3.2/Proc.html">Proc</a> represents a function pointer with an optional context (the closure data). It is typically created with a proc literal:</p> <div class="crystal highlight"><pre data-language="crystal"># A proc without parameters
-&gt;{ 1 } # Proc(Int32)

# A proc with one parameter
-&gt;(x : Int32) { x.to_s } # Proc(Int32, String)

# A proc with two parameters
-&gt;(x : Int32, y : Int32) { x + y } # Proc(Int32, Int32, Int32)
</pre></div> <p>The types of the parameters are mandatory, except when directly sending a proc literal to a lib <code>fun</code> in C bindings.</p> <p>The return type is inferred from the proc's body, but can also be provided explicitly:</p> <div class="highlight"><pre data-language="crystal"># A proc returning an Int32 or String
-&gt; : Int32 | String { 1 } # Proc(Int32 | String)

# A proc with one parameter and returning Nil
-&gt;(x : Array(String)) : Nil { x.delete("foo") } # Proc(Array(String), Nil)

# The return type must match the proc's body
-&gt;(x : Int32) : Bool { x.to_s } # Error: expected Proc to return Bool, not String
</pre></div> <p>A <code>new</code> method is provided too, which creates a <code>Proc</code> from a <a href="../capturing_blocks">captured block</a>. This form is mainly useful with <a href="../alias">aliases</a>:</p> <div class="crystal highlight"><pre data-language="crystal">Proc(Int32, String).new { |x| x.to_s } # Proc(Int32, String)

alias Foo = Int32 -&gt; String
Foo.new { |x| x.to_s } # same proc as above
</pre></div> <h2 id="the-proc-type">The Proc type</h2> <p>To denote a Proc type you can write:</p> <div class="crystal highlight"><pre data-language="crystal"># A Proc accepting a single Int32 argument and returning a String
Proc(Int32, String)

# A proc accepting no arguments and returning Nil
Proc(Nil)

# A proc accepting two arguments (one Int32 and one String) and returning a Char
Proc(Int32, String, Char)
</pre></div> <p>In type restrictions, generic type arguments and other places where a type is expected, you can use a shorter syntax, as explained in the <a href="../type_grammar">type</a>:</p> <div class="crystal highlight"><pre data-language="crystal"># An array of Proc(Int32, String, Char)
Array(Int32, String -&gt; Char)
</pre></div> <h2 id="invoking">Invoking</h2> <p>To invoke a Proc, you invoke the <code>call</code> method on it. The number of arguments must match the proc's type:</p> <div class="crystal highlight"><pre data-language="crystal">proc = -&gt;(x : Int32, y : Int32) { x + y }
proc.call(1, 2) # =&gt; 3
</pre></div> <h2 id="from-methods">From methods</h2> <p>A Proc can be created from an existing method:</p> <div class="crystal highlight"><pre data-language="crystal">def one
  1
end

proc = -&gt;one
proc.call # =&gt; 1
</pre></div> <p>If the method has parameters, you must specify their types:</p> <div class="crystal highlight"><pre data-language="crystal">def plus_one(x)
  x + 1
end

proc = -&gt;plus_one(Int32)
proc.call(41) # =&gt; 42
</pre></div> <p>A proc can optionally specify a receiver:</p> <div class="crystal highlight"><pre data-language="crystal">str = "hello"
proc = -&gt;str.count(Char)
proc.call('e') # =&gt; 1
proc.call('l') # =&gt; 2
</pre></div><div class="_attribution">
  <p class="_attribution-p">
    <a href="https://crystal-lang.org/reference/1.3/syntax_and_semantics/literals/proc.html" class="_attribution-link" target="_blank">https://crystal-lang.org/reference/1.3/syntax_and_semantics/literals/proc.html</a>
  </p>
</div>
