 <h1 id="callbacks">Callbacks</h1> <p>You can use function types in C declarations:</p> <div class="crystal highlight"><pre data-language="crystal">lib X
  # In C:
  #
  #    void callback(int (*f)(int));
  fun callback(f : Int32 -&gt; Int32)
end
</pre></div> <p>Then you can pass a function (a <a href="https://crystal-lang.org/api/1.3.2/Proc.html">Proc</a>) like this:</p> <div class="crystal highlight"><pre data-language="crystal">f = -&gt;(x : Int32) { x + 1 }
X.callback(f)
</pre></div> <p>If you define the function inline in the same call you can omit the parameter types, the compiler will add the types for you based on the <code>fun</code> signature:</p> <div class="crystal highlight"><pre data-language="crystal">X.callback -&gt;(x) { x + 1 }
</pre></div> <p>Note, however, that functions passed to C can't form closures. If the compiler detects at compile-time that a closure is being passed, an error will be issued:</p> <div class="crystal highlight"><pre data-language="crystal">y = 2
X.callback -&gt;(x) { x + y } # Error: can't send closure to C function
</pre></div> <p>If the compiler can't detect this at compile-time, an exception will be raised at runtime.</p> <p>Refer to the <a href="../type_grammar">type grammar</a> for the notation used in callbacks and procs types.</p> <p>If you want to pass <code>NULL</code> instead of a callback, just pass <code>nil</code>:</p> <div class="crystal highlight"><pre data-language="crystal"># Same as callback(NULL) in C
X.callback nil
</pre></div> <h2 id="passing-a-closure-to-a-c-function">Passing a closure to a C function</h2> <p>Most of the time a C function that allows setting a callback also provides a parameter for custom data. This custom data is then sent as an argument to the callback. For example, suppose a C function that invokes a callback at every tick, passing that tick:</p> <div class="crystal highlight"><pre data-language="crystal">lib LibTicker
  fun on_tick(callback : (Int32, Void* -&gt;), data : Void*)
end
</pre></div> <p>To properly define a wrapper for this function we must send the Proc as the callback data, and then convert that callback data to the Proc and finally invoke it.</p> <div class="crystal highlight"><pre data-language="crystal">module Ticker
  # The callback for the user doesn't have a Void*
  @@box : Pointer(Void)?

  def self.on_tick(&amp;callback : Int32 -&gt;)
    # Since Proc is a {Void*, Void*}, we can't turn that into a Void*, so we
    # "box" it: we allocate memory and store the Proc there
    boxed_data = Box.box(callback)

    # We must save this in Crystal-land so the GC doesn't collect it (*)
    @@box = boxed_data

    # We pass a callback that doesn't form a closure, and pass the boxed_data as
    # the callback data
    LibTicker.on_tick(-&gt;(tick, data) {
      # Now we turn data back into the Proc, using Box.unbox
      data_as_callback = Box(typeof(callback)).unbox(data)
      # And finally invoke the user's callback
      data_as_callback.call(tick)
    }, boxed_data)
  end
end

Ticker.on_tick do |tick|
  puts tick
end
</pre></div> <p>Note that we save the boxed callback in <code>@@box</code>. The reason is that if we don't do it, and our code doesn't reference it anymore, the GC will collect it. The C library will of course store the callback, but Crystal's GC has no way of knowing that.</p> <h2 id="raises-annotation">Raises annotation</h2> <p>If a C function executes a user-provided callback that might raise, it must be annotated with the <code>@[Raises]</code> annotation.</p> <p>The compiler infers this annotation for a method if it invokes a method that is marked as <code>@[Raises]</code> or raises (recursively).</p> <p>However, some C functions accept callbacks to be executed by other C functions. For example, suppose a fictitious library:</p> <div class="crystal highlight"><pre data-language="crystal">lib LibFoo
  fun store_callback(callback : -&gt;)
  fun execute_callback
end

LibFoo.store_callback -&gt;{ raise "OH NO!" }
LibFoo.execute_callback
</pre></div> <p>If the callback passed to <code>store_callback</code> raises, then <code>execute_callback</code> will raise. However, the compiler doesn't know that <code>execute_callback</code> can potentially raise because it is not marked as <code>@[Raises]</code> and the compiler has no way to figure this out. In these cases you have to manually mark such functions:</p> <div class="crystal highlight"><pre data-language="crystal">lib LibFoo
  fun store_callback(callback : -&gt;)

  @[Raises]
  fun execute_callback
end
</pre></div> <p>If you don't mark them, <code>begin/rescue</code> blocks that surround this function's calls won't work as expected.</p><div class="_attribution">
  <p class="_attribution-p">
    <a href="https://crystal-lang.org/reference/1.3/syntax_and_semantics/c_bindings/callbacks.html" class="_attribution-link" target="_blank">https://crystal-lang.org/reference/1.3/syntax_and_semantics/c_bindings/callbacks.html</a>
  </p>
</div>
