 <h1 id="fun">fun</h1> <p>A <code>fun</code> declaration inside a <code>lib</code> binds to a C function.</p> <div class="crystal highlight"><pre data-language="crystal">lib C
  # In C: double cos(double x)
  fun cos(value : Float64) : Float64
end
</pre></div> <p>Once you bind it, the function is available inside the <code>C</code> type as if it was a class method:</p> <div class="crystal highlight"><pre data-language="crystal">C.cos(1.5) # =&gt; 0.0707372
</pre></div> <p>You can omit the parentheses if the function doesn't have parameters (and omit them in the call as well):</p> <div class="crystal highlight"><pre data-language="crystal">lib C
  fun getch : Int32
end

C.getch
</pre></div> <p>If the return type is void you can omit it:</p> <div class="crystal highlight"><pre data-language="crystal">lib C
  fun srand(seed : UInt32)
end

C.srand(1_u32)
</pre></div> <p>You can bind to variadic functions:</p> <div class="crystal highlight"><pre data-language="crystal">lib X
  fun variadic(value : Int32, ...) : Int32
end

X.variadic(1, 2, 3, 4)
</pre></div> <p>Note that there are no implicit conversions (except <code>to_unsafe</code>, which is explained later) when invoking a C function: you must pass the exact type that is expected. For integers and floats you can use the various <code>to_...</code> methods.</p> <h2 id="function-names">Function names</h2> <p>Function names in a <code>lib</code> definition can start with an upper case letter. That's different from methods and function definitions outside a <code>lib</code>, which must start with a lower case letter.</p> <p>Function names in Crystal can be different from the C name. The following example shows how to bind the C function name <code>SDL_Init</code> as <code>LibSDL.init</code> in Crystal.</p> <div class="crystal highlight"><pre data-language="crystal">lib LibSDL
  fun init = SDL_Init(flags : UInt32) : Int32
end
</pre></div> <p>The C name can be put in quotes to be able to write a name that is not a valid identifier:</p> <div class="crystal highlight"><pre data-language="crystal">lib LLVMIntrinsics
  fun ceil_f32 = "llvm.ceil.f32"(value : Float32) : Float32
end
</pre></div> <p>This can also be used to give shorter, nicer names to C functions, as these tend to be long and are usually prefixed with the library name.</p> <h2 id="types-in-c-bindings">Types in C Bindings</h2> <p>The valid types to use in C bindings are:</p> <ul> <li>Primitive types (<code>Int8</code>, ..., <code>Int64</code>, <code>UInt8</code>, ..., <code>UInt64</code>, <code>Float32</code>, <code>Float64</code>)</li> <li>Pointer types (<code>Pointer(Int32)</code>, which can also be written as <code>Int32*</code>)</li> <li>Static arrays (<code>StaticArray(Int32, 8)</code>, which can also be written as <code>Int32[8]</code>)</li> <li>Function types (<code>Function(Int32, Int32)</code>, which can also be written as <code>Int32 -&gt; Int32</code>)</li> <li>Other <code>struct</code>, <code>union</code>, <code>enum</code>, <code>type</code> or <code>alias</code> declared previously.</li> <li>
<code>Void</code>: the absence of a return value.</li> <li>
<code>NoReturn</code>: similar to <code>Void</code>, but the compiler understands that no code can be executed after that invocation.</li> <li>Crystal structs marked with the <code>@[Extern]</code> annotation</li> </ul> <p>Refer to the <a href="../type_grammar">type grammar</a> for the notation used in fun types.</p> <p>The standard library defines the <a href="https://github.com/crystal-lang/crystal/blob/master/src/lib_c.cr">LibC</a> lib with aliases for common C types, like <code>int</code>, <code>short</code>, <code>size_t</code>. Use them in bindings like this:</p> <div class="crystal highlight"><pre data-language="crystal">lib MyLib
  fun my_fun(some_size : LibC::SizeT)
end
</pre></div> <div class="admonition note"> <p class="admonition-title">Note</p> <p>The C <code>char</code> type is <code>UInt8</code> in Crystal, so a <code>char*</code> or a <code>const char*</code> is <code>UInt8*</code>. The <code>Char</code> type in Crystal is a unicode codepoint so it is represented by four bytes, making it similar to an <code>Int32</code>, not to an <code>UInt8</code>. There's also the alias <code>LibC::Char</code> if in doubt.</p> </div><div class="_attribution">
  <p class="_attribution-p">
    <a href="https://crystal-lang.org/reference/1.3/syntax_and_semantics/c_bindings/fun.html" class="_attribution-link" target="_blank">https://crystal-lang.org/reference/1.3/syntax_and_semantics/c_bindings/fun.html</a>
  </p>
</div>
