 <h1 id="methods-and-instance-variables">Methods and instance variables</h1> <p>We can simplify our constructor by using a shorter syntax for assigning a method parameter to an instance variable:</p> <div class="crystal highlight"><pre data-language="crystal">class Person
  def initialize(@name : String)
    @age = 0
  end

  def age
    @age
  end
end
</pre></div> <p>Right now, we can't do much with a person aside from create it with a name. Its age will always be zero. So lets add a method that makes a person become older:</p> <div class="crystal highlight"><pre data-language="crystal">class Person
  def initialize(@name : String)
    @age = 0
  end

  def age
    @age
  end

  def become_older
    @age += 1
  end
end

john = Person.new "John"
peter = Person.new "Peter"

john.age # =&gt; 0

john.become_older
john.age # =&gt; 1

peter.age # =&gt; 0
</pre></div> <p>Method names begin with a lowercase letter and, as a convention, only use lowercase letters, underscores and numbers.</p> <h2 id="getters-and-setters">Getters and setters</h2> <p>The Crystal <a href="https://crystal-lang.org/api">Standard Library</a> provides macros which simplify the definition of getter and setter methods:</p> <div class="crystal highlight"><pre data-language="crystal">class Person
  property age
  getter name : String

  def initialize(@name)
    @age = 0
  end
end

john = Person.new "John"
john.age = 32
john.age # =&gt; 32
</pre></div> <p>For more information on getter and setter macros, see the standard library documentation for <a href="https://crystal-lang.org/api/latest/Object.html#getter%28%2Anames%2C%26block%29-macro">Object#getter</a>, <a href="https://crystal-lang.org/api/latest/Object.html#setter%28%2Anames%29-macro">Object#setter</a>, and <a href="https://crystal-lang.org/api/latest/Object.html#property%28%2Anames%2C%26block%29-macro">Object#property</a>.</p> <p>As a side note, we can define <code>become_older</code> inside the original <code>Person</code> definition, or in a separate definition: Crystal combines all definitions into a single class. The following works just fine:</p> <div class="crystal highlight"><pre data-language="crystal">class Person
  def initialize(@name : String)
    @age = 0
  end
end

class Person
  def become_older
    @age += 1
  end
end
</pre></div> <h2 id="redefining-methods-and-previous_def">Redefining methods, and previous_def</h2> <p>If you redefine a method, the last definition will take precedence.</p> <div class="crystal highlight"><pre data-language="crystal">class Person
  def become_older
    @age += 1
  end
end

class Person
  def become_older
    @age += 2
  end
end

person = Person.new "John"
person.become_older
person.age # =&gt; 2
</pre></div> <p>You can invoke the previously redefined method with <code>previous_def</code>:</p> <div class="crystal highlight"><pre data-language="crystal">class Person
  def become_older
    @age += 1
  end
end

class Person
  def become_older
    previous_def
    @age += 2
  end
end

person = Person.new "John"
person.become_older
person.age # =&gt; 3
</pre></div> <p>Without arguments or parentheses, <code>previous_def</code> receives all of the method's parameters as arguments. Otherwise, it receives the arguments you pass to it.</p> <h2 id="catch-all-initialization">Catch-all initialization</h2> <p>Instance variables can also be initialized outside <code>initialize</code> methods:</p> <div class="crystal highlight"><pre data-language="crystal">class Person
  @age = 0

  def initialize(@name : String)
  end
end
</pre></div> <p>This will initialize <code>@age</code> to zero in every constructor. This is useful to avoid duplication, but also to avoid the <code>Nil</code> type when reopening a class and adding instance variables to it.</p><div class="_attribution">
  <p class="_attribution-p">
    <a href="https://crystal-lang.org/reference/syntax_and_semantics/methods_and_instance_variables.html" class="_attribution-link" target="_blank">https://crystal-lang.org/reference/syntax_and_semantics/methods_and_instance_variables.html</a>
  </p>
</div>
