 <h1 id="visibility">Visibility</h1> <p>Methods are public by default: the compiler will always let you invoke them. There is no <code>public</code> keyword for this reason.</p> <p>Methods <em>can</em> be marked as <code>private</code> or <code>protected</code>.</p> <h2 id="private-methods">Private methods</h2> <p>A <code>private</code> method can only be invoked without a receiver, that is, without something before the dot. The only exception is <code>self</code> as a receiver:</p> <div class="crystal highlight"><pre data-language="crystal">class Person
  private def say(message)
    puts message
  end

  def say_hello
    say "hello"      # OK, no receiver
    self.say "hello" # OK, self is a receiver, but it's allowed.

    other = Person.new
    other.say "hello" # Error, other is a receiver
  end
end
</pre></div> <p>Note that <code>private</code> methods are visible by subclasses:</p> <div class="crystal highlight"><pre data-language="crystal">class Employee &lt; Person
  def say_bye
    say "bye" # OK
  end
end
</pre></div> <h2 id="private-types">Private types</h2> <p>Private types can only be referenced inside the namespace where they are defined, and never be fully qualified.</p> <div class="crystal highlight"><pre data-language="crystal">class Foo
  private class Bar
  end

  Bar      # OK
  Foo::Bar # Error
end

Foo::Bar # Error
</pre></div> <p><code>private</code> can be used with <code>class</code>, <code>module</code>, <code>lib</code>, <code>enum</code>, <code>alias</code> and constants:</p> <div class="crystal highlight"><pre data-language="crystal">class Foo
  private ONE = 1

  ONE # =&gt; 1
end

Foo::ONE # Error
</pre></div> <h2 id="protected-methods">Protected methods</h2> <p>A <code>protected</code> method can only be invoked on:</p> <ol> <li>instances of the same type as the current type</li> <li>instances in the same namespace (class, struct, module, etc.) as the current type</li> </ol> <div class="crystal highlight"><pre data-language="crystal"># Example of 1

class Person
  protected def say(message)
    puts message
  end

  def say_hello
    say "hello"      # OK, implicit self is a Person
    self.say "hello" # OK, self is a Person

    other = Person.new "Other"
    other.say "hello" # OK, other is a Person
  end
end

class Animal
  def make_a_person_talk
    person = Person.new
    person.say "hello" # Error: person is a Person but current type is an Animal
  end
end

one_more = Person.new "One more"
one_more.say "hello" # Error: one_more is a Person but current type is the Program

# Example of 2

module Namespace
  class Foo
    protected def foo
      puts "Hello"
    end
  end

  class Bar
    def bar
      # Works, because Foo and Bar are under Namespace
      Foo.new.foo
    end
  end
end

Namespace::Bar.new.bar
</pre></div> <p>A <code>protected</code> method can only be invoked from the scope of its class or its descendants. That includes the class scope and bodies of class methods and instance methods of the same type the protected method is defined on, as well as all types including or inherinting that type and all types in that namespace.</p> <div class="crystal highlight"><pre data-language="crystal">class Parent
  protected def self.protected_method
  end

  Parent.protected_method # OK

  def instance_method
    Parent.protected_method # OK
  end

  def self.class_method
    Parent.protected_method # OK
  end
end

class Child &lt; Parent
  Parent.protected_method # OK

  def instance_method
    Parent.protected_method # OK
  end

  def self.class_method
    Parent.protected_method # OK
  end
end

class Parent::Sub
  Parent.protected_method # OK

  def instance_method
    Parent.protected_method # OK
  end

  def self.class_method
    Parent.protected_method # OK
  end
end
</pre></div> <h2 id="private-top-level-methods">Private top-level methods</h2> <p>A <code>private</code> top-level method is only visible in the current file.</p> <div class="admonition example"> <p class="admonition-title">one.cr</p> <div class="crystal highlight"><pre data-language="crystal">private def greet
  puts "Hello"
end

greet # =&gt; "Hello"
</pre></div> </div> <div class="admonition example"> <p class="admonition-title">two.cr</p> <div class="crystal highlight"><pre data-language="crystal">require "./one"

greet # undefined local variable or method 'greet'
</pre></div> </div> <p>This allows you to define helper methods in a file that will only be known in that file.</p> <h2 id="private-top-level-types">Private top-level types</h2> <p>A <code>private</code> top-level type is only visible in the current file.</p> <div class="admonition example"> <p class="admonition-title">one.cr</p> <div class="crystal highlight"><pre data-language="crystal">private class Greeter
  def self.greet
    "Hello"
  end
end

Greeter.greet # =&gt; "Hello"
</pre></div> </div> <div class="admonition example"> <p class="admonition-title">two.cr</p> <div class="crystal highlight"><pre data-language="crystal">require "./one"

Greeter.greet # undefined constant 'Greeter'
</pre></div> </div><div class="_attribution">
  <p class="_attribution-p">
    <a href="https://crystal-lang.org/reference/syntax_and_semantics/visibility.html" class="_attribution-link" target="_blank">https://crystal-lang.org/reference/syntax_and_semantics/visibility.html</a>
  </p>
</div>
