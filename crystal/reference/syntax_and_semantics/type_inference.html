 <h1 id="type-inference">Type inference</h1> <p>Crystal's philosophy is to require as few type restrictions as possible. However, some restrictions are required.</p> <p>Consider a class definition like this:</p> <div class="crystal highlight"><pre data-language="crystal">class Person
  def initialize(@name)
    @age = 0
  end
end
</pre></div> <p>We can quickly see that <code>@age</code> is an integer, but we don't know the type of <code>@name</code>. The compiler could infer its type from all uses of the <code>Person</code> class. However, doing so has a few issues:</p> <ul> <li>The type is not obvious for a human reading the code: they would also have to check all uses of <code>Person</code> to find this out.</li> <li>Some compiler optimizations, like having to analyze a method just once, and incremental compilation, are nearly impossible to do.</li> </ul> <p>As a code base grows, these issues gain more relevance: understanding a project becomes harder, and compile times become unbearable.</p> <p>For this reason, Crystal needs to know, in an obvious way (as obvious as to a human), the types of instance and <a href="class_variables">class</a> variables.</p> <p>There are several ways to let Crystal know this.</p> <h2 id="with-type-restrictions">With type restrictions</h2> <p>The easiest, but probably most tedious, way is to use explicit type restrictions.</p> <div class="crystal highlight"><pre data-language="crystal">class Person
  @name : String
  @age : Int32

  def initialize(@name)
    @age = 0
  end
end
</pre></div> <h2 id="without-type-restrictions">Without type restrictions</h2> <p>If you omit an explicit type restriction, the compiler will try to infer the type of instance and class variables using a bunch of syntactic rules.</p> <p>For a given instance/class variable, when a rule can be applied and a type can be guessed, the type is added to a set. When no more rules can be applied, the inferred type will be the <a href="union_types">union</a> of those types. Additionally, if the compiler infers that an instance variable isn't always initialized, it will also include the <a href="literals/nil">Nil</a> type.</p> <p>The rules are many, but usually the first three are most used. There's no need to remember them all. If the compiler gives an error saying that the type of an instance variable can't be inferred you can always add an explicit type restriction.</p> <p>The following rules only mention instance variables, but they apply to class variables as well. They are:</p> <h3 id="1-assigning-a-literal-value">1. Assigning a literal value</h3> <p>When a literal is assigned to an instance variable, the literal's type is added to the set. All <a href="literals/index">literals</a> have an associated type.</p> <p>In the following example, <code>@name</code> is inferred to be <code>String</code> and <code>@age</code> to be <code>Int32</code>.</p> <div class="crystal highlight"><pre data-language="crystal">class Person
  def initialize
    @name = "John Doe"
    @age = 0
  end
end
</pre></div> <p>This rule, and every following rule, will also be applied in methods other than <code>initialize</code>. For example:</p> <div class="crystal highlight"><pre data-language="crystal">class SomeObject
  def lucky_number
    @lucky_number = 42
  end
end
</pre></div> <p>In the above case, <code>@lucky_number</code> will be inferred to be <code>Int32 | Nil</code>: <code>Int32</code> because 42 was assigned to it, and <code>Nil</code> because it wasn't assigned in all of the class' initialize methods.</p> <h3 id="2-assigning-the-result-of-invoking-the-class-method-new">2. Assigning the result of invoking the class method <code>new</code>
</h3> <p>When an expression like <code>Type.new(...)</code> is assigned to an instance variable, the type <code>Type</code> is added to the set.</p> <p>In the following example, <code>@address</code> is inferred to be <code>Address</code>.</p> <div class="crystal highlight"><pre data-language="crystal">class Person
  def initialize
    @address = Address.new("somewhere")
  end
end
</pre></div> <p>This also is applied to generic types. Here <code>@values</code> is inferred to be <code>Array(Int32)</code>.</p> <div class="crystal highlight"><pre data-language="crystal">class Something
  def initialize
    @values = Array(Int32).new
  end
end
</pre></div> <p><strong>Note</strong>: a <code>new</code> method might be redefined by a type. In that case the inferred type will be the one returned by <code>new</code>, if it can be inferred using some of the next rules.</p> <h3 id="3-assigning-a-variable-that-is-a-method-parameter-with-a-type-restriction">3. Assigning a variable that is a method parameter with a type restriction</h3> <p>In the following example <code>@name</code> is inferred to be <code>String</code> because the method parameter <code>name</code> has a type restriction of type <code>String</code>, and that parameter is assigned to <code>@name</code>.</p> <div class="crystal highlight"><pre data-language="crystal">class Person
  def initialize(name : String)
    @name = name
  end
end
</pre></div> <p>Note that the name of the method parameter is not important; this works as well:</p> <div class="crystal highlight"><pre data-language="crystal">class Person
  def initialize(obj : String)
    @name = obj
  end
end
</pre></div> <p>Using the shorter syntax to assign an instance variable from a method parameter has the same effect:</p> <div class="crystal highlight"><pre data-language="crystal">class Person
  def initialize(@name : String)
  end
end
</pre></div> <p>Also note that the compiler doesn't check whether a method parameter is reassigned a different value:</p> <div class="crystal highlight"><pre data-language="crystal">class Person
  def initialize(name : String)
    name = 1
    @name = name
  end
end
</pre></div> <p>In the above case, the compiler will still infer <code>@name</code> to be <code>String</code>, and later will give a compile time error, when fully typing that method, saying that <code>Int32</code> can't be assigned to a variable of type <code>String</code>. Use an explicit type restriction if <code>@name</code> isn't supposed to be a <code>String</code>.</p> <h3 id="4-assigning-the-result-of-a-class-method-that-has-a-return-type-restriction">4. Assigning the result of a class method that has a return type restriction</h3> <p>In the following example, <code>@address</code> is inferred to be <code>Address</code>, because the class method <code>Address.unknown</code> has a return type restriction of <code>Address</code>.</p> <div class="crystal highlight"><pre data-language="crystal">class Person
  def initialize
    @address = Address.unknown
  end
end

class Address
  def self.unknown : Address
    new("unknown")
  end

  def initialize(@name : String)
  end
end
</pre></div> <p>In fact, the above code doesn't need the return type restriction in <code>self.unknown</code>. The reason is that the compiler will also look at a class method's body and if it can apply one of the previous rules (it's a <code>new</code> method, or it's a literal, etc.) it will infer the type from that expression. So, the above can be simply written like this:</p> <div class="crystal highlight"><pre data-language="crystal">class Person
  def initialize
    @address = Address.unknown
  end
end

class Address
  # No need for a return type restriction here
  def self.unknown
    new("unknown")
  end

  def initialize(@name : String)
  end
end
</pre></div> <p>This extra rule is very convenient because it's very common to have "constructor-like" class methods in addition to <code>new</code>.</p> <h3 id="5-assigning-a-variable-that-is-a-method-parameter-with-a-default-value">5. Assigning a variable that is a method parameter with a default value</h3> <p>In the following example, because the default value of <code>name</code> is a string literal, and it's later assigned to <code>@name</code>, <code>String</code> will be added to the set of inferred types.</p> <div class="crystal highlight"><pre data-language="crystal">class Person
  def initialize(name = "John Doe")
    @name = name
  end
end
</pre></div> <p>This of course also works with the short syntax:</p> <div class="crystal highlight"><pre data-language="crystal">class Person
  def initialize(@name = "John Doe")
  end
end
</pre></div> <p>The default parameter value can also be a <code>Type.new(...)</code> method or a class method with a return type restriction.</p> <h3 id="6-assigning-the-result-of-invoking-a-lib-function">6. Assigning the result of invoking a <code>lib</code> function</h3> <p>Because a <a href="c_bindings/fun">lib function</a> must have explicit types, the compiler can use the return type when assigning it to an instance variable.</p> <p>In the following example <code>@age</code> is inferred to be <code>Int32</code>.</p> <div class="crystal highlight"><pre data-language="crystal">class Person
  def initialize
    @age = LibPerson.compute_default_age
  end
end

lib LibPerson
  fun compute_default_age : Int32
end
</pre></div> <h3 id="7-using-an-out-lib-expression">7. Using an <code>out</code> lib expression</h3> <p>Because a <a href="c_bindings/fun">lib function</a> must have explicit types, the compiler can use the <code>out</code> argument's type, which should be a pointer type, and use the dereferenced type as a guess.</p> <p>In the following example <code>@age</code> is inferred to be <code>Int32</code>.</p> <div class="crystal highlight"><pre data-language="crystal">class Person
  def initialize
    LibPerson.compute_default_age(out @age)
  end
end

lib LibPerson
  fun compute_default_age(age_ptr : Int32*)
end
</pre></div> <h3 id="other-rules">Other rules</h3> <p>The compiler will try to be as smart as possible to require less explicit type restrictions. For example, if assigning an <code>if</code> expression, type will be inferred from the <code>then</code> and <code>else</code> branches:</p> <div class="crystal highlight"><pre data-language="crystal">class Person
  def initialize
    @age = some_condition ? 1 : 2
  end
end
</pre></div> <p>Because the <code>if</code> above (well, technically a ternary operator, but it's similar to an <code>if</code>) has integer literals, <code>@age</code> is successfully inferred to be <code>Int32</code> without requiring a redundant type restriction.</p> <p>Another case is <code>||</code> and <code>||=</code>:</p> <div class="crystal highlight"><pre data-language="crystal">class SomeObject
  def lucky_number
    @lucky_number ||= 42
  end
end
</pre></div> <p>In the above example <code>@lucky_number</code> will be inferred to be <code>Int32 | Nil</code>. This is very useful for lazily initialized variables.</p> <p>Constants will also be followed, as it's pretty simple for the compiler (and a human) to do so.</p> <div class="crystal highlight"><pre data-language="crystal">class SomeObject
  DEFAULT_LUCKY_NUMBER = 42

  def initialize(@lucky_number = DEFAULT_LUCKY_NUMBER)
  end
end
</pre></div> <p>Here rule 5 (default parameter value) is used, and because the constant resolves to an integer literal, <code>@lucky_number</code> is inferred to be <code>Int32</code>.</p><div class="_attribution">
  <p class="_attribution-p">
    <a href="https://crystal-lang.org/reference/syntax_and_semantics/type_inference.html" class="_attribution-link" target="_blank">https://crystal-lang.org/reference/syntax_and_semantics/type_inference.html</a>
  </p>
</div>
