 <h1 id="struct">struct</h1> <p>A <code>struct</code> declaration inside a <code>lib</code> declares a C struct.</p> <div class="crystal highlight"><pre data-language="crystal">lib C
  # In C:
  #
  #  struct TimeZone {
  #    int minutes_west;
  #    int dst_time;
  #  };
  struct TimeZone
    minutes_west : Int32
    dst_time : Int32
  end
end
</pre></div> <p>You can also specify many fields of the same type:</p> <div class="crystal highlight"><pre data-language="crystal">lib C
  struct TimeZone
    minutes_west, dst_time : Int32
  end
end
</pre></div> <p>Recursive structs work just like you expect them to:</p> <div class="crystal highlight"><pre data-language="crystal">lib C
  struct LinkedListNode
    prev, _next : LinkedListNode*
  end

  struct LinkedList
    head : LinkedListNode*
  end
end
</pre></div> <p>To create an instance of a struct use <code>new</code>:</p> <div class="crystal highlight"><pre data-language="crystal">tz = C::TimeZone.new
</pre></div> <p>This allocates the struct on the stack.</p> <p>A C struct starts with all its fields set to "zero": integers and floats start at zero, pointers start with an address of zero, etc.</p> <p>To avoid this initialization you can use <code>uninitialized</code>:</p> <div class="crystal highlight"><pre data-language="crystal">tz = uninitialized C::TimeZone
tz.minutes_west # =&gt; some garbage value
</pre></div> <p>You can set and get its properties:</p> <div class="crystal highlight"><pre data-language="crystal">tz = C::TimeZone.new
tz.minutes_west = 1
tz.minutes_west # =&gt; 1
</pre></div> <p>If the assigned value is not exactly the same as the property's type, <a href="to_unsafe">to_unsafe</a> will be tried.</p> <p>You can also initialize some fields with a syntax similar to <a href="../default_and_named_arguments">named arguments</a>:</p> <div class="crystal highlight"><pre data-language="crystal">tz = C::TimeZone.new minutes_west: 1, dst_time: 2
tz.minutes_west # =&gt; 1
tz.dst_time     # =&gt; 2
</pre></div> <p>A C struct is passed by value (as a copy) to functions and methods, and also passed by value when it is returned from a method:</p> <div class="crystal highlight"><pre data-language="crystal">def change_it(tz)
  tz.minutes_west = 1
end

tz = C::TimeZone.new
change_it tz
tz.minutes_west # =&gt; 0
</pre></div> <p>Refer to the <a href="../type_grammar">type grammar</a> for the notation used in struct field types.</p><div class="_attribution">
  <p class="_attribution-p">
    <a href="https://crystal-lang.org/reference/syntax_and_semantics/c_bindings/struct.html" class="_attribution-link" target="_blank">https://crystal-lang.org/reference/syntax_and_semantics/c_bindings/struct.html</a>
  </p>
</div>
