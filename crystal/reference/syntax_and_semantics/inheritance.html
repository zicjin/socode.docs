 <h1 id="inheritance">Inheritance</h1> <p>Every class except <code>Object</code>, the hierarchy root, inherits from another class (its superclass). If you don't specify one it defaults to <code>Reference</code> for classes and <code>Struct</code> for structs.</p> <p>A class inherits all instance variables and all instance and class methods of a superclass, including its constructors (<code>new</code> and <code>initialize</code>).</p> <div class="crystal highlight"><pre data-language="crystal">class Person
  def initialize(@name : String)
  end

  def greet
    puts "Hi, I'm #{@name}"
  end
end

class Employee &lt; Person
end

employee = Employee.new "John"
employee.greet # "Hi, I'm John"
</pre></div> <p>If a class defines a <code>new</code> or <code>initialize</code> then its superclass constructors are not inherited:</p> <div class="crystal highlight"><pre data-language="crystal">class Person
  def initialize(@name : String)
  end
end

class Employee &lt; Person
  def initialize(@name : String, @company_name : String)
  end
end

Employee.new "John", "Acme" # OK
Employee.new "Peter"        # Error: wrong number of arguments for 'Employee:Class#new' (1 for 2)
</pre></div> <p>You can override methods in a derived class:</p> <div class="crystal highlight"><pre data-language="crystal">class Person
  def greet(msg)
    puts "Hi, #{msg}"
  end
end

class Employee &lt; Person
  def greet(msg)
    puts "Hello, #{msg}"
  end
end

p = Person.new
p.greet "everyone" # "Hi, everyone"

e = Employee.new
e.greet "everyone" # "Hello, everyone"
</pre></div> <p>Instead of overriding you can define specialized methods by using type restrictions:</p> <div class="crystal highlight"><pre data-language="crystal">class Person
  def greet(msg)
    puts "Hi, #{msg}"
  end
end

class Employee &lt; Person
  def greet(msg : Int32)
    puts "Hi, this is a number: #{msg}"
  end
end

e = Employee.new
e.greet "everyone" # "Hi, everyone"

e.greet 1 # "Hi, this is a number: 1"
</pre></div> <h2 id="super">super</h2> <p>You can invoke a superclass' method using <code>super</code>:</p> <div class="crystal highlight"><pre data-language="crystal">class Person
  def greet(msg)
    puts "Hello, #{msg}"
  end
end

class Employee &lt; Person
  def greet(msg)
    super # Same as: super(msg)
    super("another message")
  end
end
</pre></div> <p>Without arguments or parentheses, <code>super</code> receives all of the method's parameters as arguments. Otherwise, it receives the arguments you pass to it.</p> <h2 id="covariance-and-contravariance">Covariance and Contravariance</h2> <p>One place inheritance can get a little tricky is with arrays. We have to be careful when declaring an array of objects where inheritance is used. For example, consider the following</p> <div class="crystal highlight"><pre data-language="crystal">class Foo
end

class Bar &lt; Foo
end

foo_arr = [Bar.new] of Foo  # =&gt; [#&lt;Bar:0x10215bfe0&gt;] : Array(Foo)
bar_arr = [Bar.new]         # =&gt; [#&lt;Bar:0x10215bfd0&gt;] : Array(Bar)
bar_arr2 = [Foo.new] of Bar # compiler error
</pre></div> <p>A Foo array can hold both Foo's and Bar's, but an array of Bar can only hold Bar and its subclasses.</p> <p>One place this might trip you up is when automatic casting comes into play. For example, the following won't work:</p> <div class="crystal highlight"><pre data-language="crystal">class Foo
end

class Bar &lt; Foo
end

class Test
  @arr : Array(Foo)

  def initialize
    @arr = [Bar.new]
  end
end
</pre></div> <p>we've declared <code>@arr</code> as type <code>Array(Foo)</code> so we may be tempted to think that we can start putting <code>Bar</code>s in there. Not quite. In the <code>initialize</code>, the type of the <code>[Bar.new]</code> expression is <code>Array(Bar)</code>, period. And <code>Array(Bar)</code> is <em>not</em> assignable to an <code>Array(Foo)</code> instance var.</p> <p>What's the right way to do this? Change the expression so that it <em>is</em> of the right type: <code>Array(Foo)</code> (see example above).</p> <div class="crystal highlight"><pre data-language="crystal">class Foo
end

class Bar &lt; Foo
end

class Test
  @arr : Array(Foo)

  def initialize
    @arr = [Bar.new] of Foo
  end
end
</pre></div> <p>This is just one type (Array) and one operation (assignment), the logic of the above will be applied differently for other types and assignments, in general <a href="https://en.wikipedia.org/wiki/Covariance_and_contravariance_%28computer_science%29">Covariance and Contravariance</a> is not fully supported.</p><div class="_attribution">
  <p class="_attribution-p">
    <a href="https://crystal-lang.org/reference/syntax_and_semantics/inheritance.html" class="_attribution-link" target="_blank">https://crystal-lang.org/reference/syntax_and_semantics/inheritance.html</a>
  </p>
</div>
