 <h1 id="methods">Methods</h1> <p>To avoid duplication of the same message, instead of using a variable we can define a method and call it multiple times.</p> <p>A method definition is indicated by the keyword <code>def</code> followed by the method name. Every expression until the keyword <code>end</code> is part of the method body.</p> <div class="crystal-play highlight"><pre data-language="crystal">def say_hello
  puts "Hello Penny!"
end

say_hello
say_hello
say_hello() # syntactically equivalent method call with parentheses
</pre></div> <div class="admonition tip"> <p class="admonition-title">Tip</p> <p>Method calls are unambiguously indicated by parentheses after the name, but they can be omitted. It would only be necessary for disambiguation, for example, if <code>say_hello</code> was also a local variable.</p> </div> <h2 id="arguments">Arguments</h2> <p>What if we want to greet different people, but all in the same manner? Instead of writing individual messages, we can define a method that allows customization through a parameter. A parameter is like a local variable inside the method body. Parameters are declared after the method name in parentheses. When calling a method, you can pass in arguments that are mapped as values for the method's parameters.</p> <div class="crystal-play highlight"><pre data-language="crystal">def say_hello(recipient)
  puts "Hello #{recipient}!"
end

say_hello "World"
say_hello "Crystal"
</pre></div> <div class="admonition tip"> <p class="admonition-title">Tip</p> <p>Arguments at method calls are typically placed in parentheses, but it can often be omitted. <code>say_hello "World"</code> and <code>say_hello("World")</code> are syntactically equivalent.</p> <p>It's generally recommended to use parentheses because it avoids ambiguity. But they're often omitted if the expression reads like natural language.</p> </div> <h3 id="default-arguments">Default arguments</h3> <p>Arguments can be assigned a default value. It is used in case the argument is missing in the method call. Usually, arguments are mandatory but when there's a default value, it can be omitted.</p> <div class="crystal-play highlight"><pre data-language="crystal">def say_hello(recipient = "World")
  puts "Hello #{recipient}!"
end

say_hello
say_hello "Crystal"
</pre></div> <h2 id="type-restrictions">Type Restrictions</h2> <p>Our example method expects <code>recipient</code> to be a <code>String</code>. But any other type would work as well. Try <code>say_hello 6</code> for example.</p> <p>This isn't necessarily a problem for this method. Using any other type would be valid code. But semantically we want to greet people with a name as a <code>String</code>.</p> <p>Type restrictions limit the allowed type of an argument. They come after the argument name, separated by a colon:</p> <div class="crystal-play highlight"><pre data-language="crystal">def say_hello(recipient : String)
  puts "Hello #{recipient}!"
end

say_hello "World"
say_hello "Crystal"

# Now this expression doesn't compile:
# say_hello 6
</pre></div> <p>Now names cannot be numbers or other data types anymore. This doesn't mean you can't greet people with a number as a name. The number just needs to be expressed as a string. Try <code>say_hello "6"</code> for example.</p> <h2 id="overloading">Overloading</h2> <p>Restricting the type of an argument can be used for positional overloading. When a method has an unrestricted argument like <code>say_hello(recipient)</code>, <em>all</em> calls to a method <code>say_hello</code> go to that method. But with overloading several methods of the same name can exist with different argument type restrictions. Each call is routed to the most fitting overload.</p> <div class="crystal-play highlight"><pre data-language="crystal"># This methods greets *recipient*.
def say_hello(recipient : String)
  puts "Hello #{recipient}!"
end

# This method greets *times* times.
def say_hello(times : Int32)
  puts "Hello " * times
end

say_hello "World"
say_hello 3
</pre></div> <p>Overloading isn't defined just by type restrictions. The number of arguments as well as named arguments are also relevant characteristics.</p> <h2 id="returning-a-value">Returning a value</h2> <p>Methods return a value which becomes the value of the method call. By default, it's the value of the last expression in the method:</p> <div class="crystal-play highlight"><pre data-language="crystal">def adds_2(n : Int32)
  n + 2
end

puts adds_2 40
</pre></div> <p>A method can return at any place in its body using the <code>return</code> statement. The argument passed to <code>return</code> becomes the method's return value. If there is no argument, it's <code>nil</code>.</p> <p>The following example illustrates the use of an <em>explicit</em> and an <em>implicit</em> <code>return</code>:</p> <div class="crystal-play highlight"><pre data-language="crystal"># This method returns:
# - the same number if it's even,
# - the number multiplied by 2 if it's odd.
def build_even_number(n : Int32)
  return n if n.even?

  n * 2
end

puts build_even_number 7
puts build_even_number 28
</pre></div> <h3 id="return-type">Return type</h3> <p>Let's begin defining a method that we expect it will return an <code>Int32</code> value, but mistakenly returns a <code>String</code>:</p> <div class="crystal highlight"><pre data-language="crystal">def life_universe_and_everything
  "Fortytwo"
end

puts life_universe_and_everything + 1 # Error: no overload matches 'String#+' with type Int32
</pre></div> <p>Because we never told the compiler we were expecting the method to return an <code>Int32</code>, the best the compiler can do is to tell us that there is no <code>String#+</code> method that takes an <code>Int32</code> value as an argument (i.e. the compiler is pointing at the moment when we use the value but not at the root of the bug: the type of the method's return value).</p> <p>The error message can be more accurate if using type information, so let's try again the example but now specifying the type:</p> <div class="crystal highlight"><pre data-language="crystal">def life_universe_and_everything : Int32
  "Fortytwo"
end

puts life_universe_and_everything + 1 # Error: method top-level life_universe_and_everything must return Int32 but it is returning String
</pre></div> <p>Now the compiler can show us exactly where the problem is originated. As we can see, providing type information is really useful for finding errors at compile time.</p><div class="_attribution">
  <p class="_attribution-p">
    <a href="https://crystal-lang.org/reference/1.5/tutorials/basics/60_methods.html" class="_attribution-link" target="_blank">https://crystal-lang.org/reference/1.5/tutorials/basics/60_methods.html</a>
  </p>
</div>
