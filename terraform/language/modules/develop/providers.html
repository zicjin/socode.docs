 <h1 id="providers-within-modules">  Providers Within Modules </h1> <p>In a configuration with multiple modules, there are some special considerations for how resources are associated with provider configurations.</p> <p>Each resource in the configuration must be associated with one provider configuration. Provider configurations, unlike most other concepts in Terraform, are global to an entire Terraform configuration and can be shared across module boundaries. Provider configurations can be defined only in a root Terraform module.</p> <p>Providers can be passed down to descendent modules in two ways: either <em>implicitly</em> through inheritance, or <em>explicitly</em> via the <code>providers</code> argument within a <code>module</code> block. These two options are discussed in more detail in the following sections.</p> <p>A module intended to be called by one or more other modules must not contain any <code>provider</code> blocks. A module containing its own provider configurations is not compatible with the <code>for_each</code>, <code>count</code>, and <code>depends_on</code> arguments that were introduced in Terraform v0.13. For more information, see <a href="#legacy-shared-modules-with-provider-configurations">Legacy Shared Modules with Provider Configurations</a>.</p> <p>Provider configurations are used for all operations on associated resources, including destroying remote objects and refreshing state. Terraform retains, as part of its state, a reference to the provider configuration that was most recently used to apply changes to each resource. When a <code>resource</code> block is removed from the configuration, this record in the state will be used to locate the appropriate configuration because the resource's <code>provider</code> argument (if any) will no longer be present in the configuration.</p> <p>As a consequence, you must ensure that all resources that belong to a particular provider configuration are destroyed before you can remove that provider configuration's block from your configuration. If Terraform finds a resource instance tracked in the state whose provider configuration block is no longer available then it will return an error during planning, prompting you to reintroduce the provider configuration.</p> <h2 id="provider-version-constraints-in-modules">  Provider Version Constraints in Modules </h2> <p>Although provider <em>configurations</em> are shared between modules, each module must declare its own <a href="../../providers/requirements">provider requirements</a>, so that Terraform can ensure that there is a single version of the provider that is compatible with all modules in the configuration and to specify the <a href="../../providers/requirements#source-addresses">source address</a> that serves as the global (module-agnostic) identifier for a provider.</p> <p>To declare that a module requires particular versions of a specific provider, use a <code>required_providers</code> block inside a <code>terraform</code> block:</p> <pre data-language="ruby">terraform {
  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "&gt;= 2.7.0"
    }
  }
}
</pre>
<p>A provider requirement says, for example, "This module requires version v2.7.0 of the provider <code>hashicorp/aws</code> and will refer to it as <code>aws</code>." It doesn't, however, specify any of the configuration settings that determine what remote endpoints the provider will access, such as an AWS region; configuration settings come from provider <em>configurations</em>, and a particular overall Terraform configuration can potentially have <a href="../../providers/configuration#alias-multiple-provider-configurations">several different configurations for the same provider</a>.</p> <h2 id="provider-aliases-within-modules">  Provider Aliases Within Modules </h2> <p>To declare multiple configuration names for a provider within a module, add the <code>configuration_aliases</code> argument:</p> <pre data-language="ruby">terraform {
  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "&gt;= 2.7.0"
      configuration_aliases = [ aws.alternate ]
    }
  }
}
</pre>
<p>The above requirements are identical to the previous, with the addition of the alias provider configuration name <code>aws.alternate</code>, which can be referenced by resources using the <code>provider</code> argument.</p> <p>If you are writing a shared Terraform module, constrain only the minimum required provider version using a <code>&gt;=</code> constraint. This should specify the minimum version containing the features your module relies on, and thus allow a user of your module to potentially select a newer provider version if other features are needed by other parts of their overall configuration.</p> <h2 id="implicit-provider-inheritance">  Implicit Provider Inheritance </h2> <p>For convenience in simple configurations, a child module automatically inherits default (un-aliased) provider configurations from its parent. This means that explicit <code>provider</code> blocks appear only in the root module, and downstream modules can simply declare resources for that provider and have them automatically associated with the root provider configurations.</p> <p>For example, the root module might contain only a <code>provider</code> block and a <code>module</code> block to instantiate a child module:</p> <pre data-language="ruby">provider "aws" {
  region = "us-west-1"
}

module "child" {
  source = "./child"
}
</pre>
<p>The child module can then use any resource from this provider with no further provider configuration required:</p> <pre data-language="ruby">resource "aws_s3_bucket" "example" {
  bucket = "provider-inherit-example"
}
</pre>
<p>We recommend using this approach when a single configuration for each provider is sufficient for an entire configuration.</p> <blockquote class="alert alert-warning" role="alert"> <p><strong>Note:</strong> Only provider configurations are inherited by child modules, not provider source or version requirements. Each module must <a href="../../providers/requirements">declare its own provider requirements</a>. This is especially important for non-HashiCorp providers.</p> </blockquote> <p>In more complex situations there may be <a href="../../providers/configuration#alias-multiple-provider-configurations">multiple provider configurations</a>, or a child module may need to use different provider settings than its parent. For such situations, you must pass providers explicitly.</p> <h2 id="passing-providers-explicitly">  Passing Providers Explicitly </h2> <p>When child modules each need a different configuration of a particular provider, or where the child module requires a different provider configuration than its parent, you can use the <code>providers</code> argument within a <code>module</code> block to explicitly define which provider configurations are available to the child module. For example:</p> <pre data-language="ruby"># The default "aws" configuration is used for AWS resources in the root
# module where no explicit provider instance is selected.
provider "aws" {
  region = "us-west-1"
}

# An alternate configuration is also defined for a different
# region, using the alias "usw2".
provider "aws" {
  alias  = "usw2"
  region = "us-west-2"
}

# An example child module is instantiated with the alternate configuration,
# so any AWS resources it defines will use the us-west-2 region.
module "example" {
  source    = "./example"
  providers = {
    aws = aws.usw2
  }
}
</pre>
<p>The <code>providers</code> argument within a <code>module</code> block is similar to <a href="../../meta-arguments/resource-provider">the <code>provider</code> argument</a> within a resource, but is a map rather than a single string because a module may contain resources from many different providers.</p> <p>The keys of the <code>providers</code> map are provider configuration names as expected by the child module, and the values are the names of corresponding configurations in the <em>current</em> module.</p> <p>Once the <code>providers</code> argument is used in a <code>module</code> block, it overrides all of the default inheritance behavior, so it is necessary to enumerate mappings for <em>all</em> of the required providers. This is to avoid confusion and surprises that may result when mixing both implicit and explicit provider passing.</p> <p>Additional provider configurations (those with the <code>alias</code> argument set) are <em>never</em> inherited automatically by child modules, and so must always be passed explicitly using the <code>providers</code> map. For example, a module that configures connectivity between networks in two AWS regions is likely to need both a source and a destination region. In that case, the root module may look something like this:</p> <pre data-language="ruby">provider "aws" {
  alias  = "usw1"
  region = "us-west-1"
}

provider "aws" {
  alias  = "usw2"
  region = "us-west-2"
}

module "tunnel" {
  source    = "./tunnel"
  providers = {
    aws.src = aws.usw1
    aws.dst = aws.usw2
  }
}
</pre>
<p>The subdirectory <code>./tunnel</code> must then declare the configuration aliases for the provider so the calling module can pass configurations with these names in its <code>providers</code> argument:</p> <pre data-language="ruby">terraform {
  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "&gt;= 2.7.0"
      configuration_aliases = [ aws.src, aws.dest ]
    }
  }
}
</pre>
<p>Each resource should then have its own <code>provider</code> attribute set to either <code>aws.src</code> or <code>aws.dst</code> to choose which of the two provider configurations to use.</p> <h2 id="legacy-shared-modules-with-provider-configurations">  Legacy Shared Modules with Provider Configurations </h2> <p>In Terraform v0.10 and earlier there was no explicit way to use different configurations of a provider in different modules in the same configuration, and so module authors commonly worked around this by writing <code>provider</code> blocks directly inside their modules, making the module have its own separate provider configurations separate from those declared in the root module.</p> <p>However, that pattern had a significant drawback: because a provider configuration is required to destroy the remote object associated with a resource instance as well as to create or update it, a provider configuration must always stay present in the overall Terraform configuration for longer than all of the resources it manages. If a particular module includes both resources and the provider configurations for those resources then removing the module from its caller would violate that constraint: both the resources and their associated providers would, in effect, be removed simultaneously.</p> <p>Terraform v0.11 introduced the mechanisms described in earlier sections to allow passing provider configurations between modules in a structured way, and thus we explicitly recommended against writing a child module with its own provider configuration blocks. However, that legacy pattern continued to work for compatibility purposes -- though with the same drawback -- until Terraform v0.13.</p> <p>Terraform v0.13 introduced the possibility for a module itself to use the <code>for_each</code>, <code>count</code>, and <code>depends_on</code> arguments, but the implementation of those unfortunately conflicted with the support for the legacy pattern.</p> <p>To retain the backward compatibility as much as possible, Terraform v0.13 continues to support the legacy pattern for module blocks that do not use these new features, but a module with its own provider configurations is not compatible with <code>for_each</code>, <code>count</code>, or <code>depends_on</code>. Terraform will produce an error if you attempt to combine these features. For example:</p> <pre>Error: Module does not support count

  on main.tf line 15, in module "child":
  15:   count = 2

Module "child" cannot be used with count because it contains a nested provider
configuration for "aws", at child/main.tf:2,10-15.

This module can be made compatible with count by changing it to receive all of
its provider configurations from the calling module, by using the "providers"
argument in the calling module block.
</pre>
<p>To make a module compatible with the new features, you must remove all of the <code>provider</code> blocks from its definition.</p> <p>If the new version of the module declares <code>configuration_aliases</code>, or if the calling module needs the child module to use different provider configurations than its own default provider configurations, the calling module must then include an explicit <code>providers</code> argument to describe which provider configurations the child module will use:</p> <pre data-language="ruby">provider "aws" {
  region = "us-west-1"
}

provider "aws" {
  region = "us-east-1"
  alias  = "east"
}

module "child" {
  count = 2
  providers = {
    # By default, the child module would use the
    # default (unaliased) AWS provider configuration
    # using us-west-1, but this will override it
    # to use the additional "east" configuration
    # for its resources instead.
    aws = aws.east
  }
}
</pre>
<p>Since the association between resources and provider configurations is static, module calls using <code>for_each</code> or <code>count</code> cannot pass different provider configurations to different instances. If you need different instances of your module to use different provider configurations then you must use a separate <code>module</code> block for each distinct set of provider configurations:</p> <pre data-language="ruby">provider "aws" {
  alias  = "usw1"
  region = "us-west-1"
}

provider "aws" {
  alias  = "usw2"
  region = "us-west-2"
}

provider "google" {
  alias       = "usw1"
  credentials = "${file("account.json")}"
  project     = "my-project-id"
  region      = "us-west1"
  zone        = "us-west1-a"
}

provider "google" {
  alias       = "usw2"
  credentials = "${file("account.json")}"
  project     = "my-project-id"
  region      = "us-west2"
  zone        = "us-west2-a"
}

module "bucket_w1" {
  source    = "./publish_bucket"
  providers = {
    aws.src    = aws.usw1
    google.src = google.usw2
  }
}

module "bucket_w2" {
  source    = "./publish_bucket"
  providers = {
    aws.src    = aws.usw2
    google.src = google.usw2
  }
}
</pre><div class="_attribution">
  <p class="_attribution-p">
    <a href="https://www.terraform.io/docs/language/modules/develop/providers.html" class="_attribution-link" target="_blank">https://www.terraform.io/docs/language/modules/develop/providers.html</a>
  </p>
</div>
